/*
 * Copyright (c) 2017-2024 The Forge Interactive Inc.
 * 
 * This file is part of The-Forge
 * (see https://github.com/ConfettiFX/The-Forge).
 * 
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 * 
 *   http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
*/

// USERMACRO: SAMPLE_COUNT [1,2,4]
// USERMACRO: USE_AMBIENT_OCCLUSION [0,1]
// Uncomment this definition to use ray differentials method for calculating
// gradients instead of screen-space projected triangles method.
#define USE_RAY_DIFFERENTIALS

#include "shading.h.fsl"
#include "vrs_resources.h.fsl"
#include "../../../../../../Common_3/Renderer/VisibilityBuffer/Shaders/FSL/vb_shading_utilities.h.fsl"

// This shader loads draw / triangle Id per pixel and reconstruct interpolated vertex data.

STRUCT(VSOutput)
{
	DATA(float4, position, SV_Position);
	DATA(float2, screenPos, TEXCOORD0);
};

struct TransparentNodeOIT
{
	uint triangleData; 
	uint next; 
};

struct NodeFinalOIT
{
	float3 color;
	float  alpha;
	float  depth;
	uint   next; 
};

struct ShadedFragment
{
	float4 color;
	float depth;
};

// Static descriptors
#if(SAMPLE_COUNT > 1)
	RES(Tex2DMS(float4, SAMPLE_COUNT), vbTex, UPDATE_FREQ_NONE, t0, binding = 14);
#else
	RES(Tex2D(float4), vbTex, UPDATE_FREQ_NONE, t0, binding = 14);
#endif

RES(Buffer(uint),               headIndexBufferSRV, UPDATE_FREQ_NONE, t30, binding = 15); 
RES(Buffer(TransparentNodeOIT), vbDepthLinkedListSRV, UPDATE_FREQ_NONE, t31, binding = 16); 

RES(Tex2D(float), shadowMap, UPDATE_FREQ_NONE, t101, binding = 18);

#if defined(METAL) || defined(ORBIS) || defined(PROSPERO)
	RES(Tex2D(float4), diffuseMaps[MAX_TEXTURE_UNITS],  UPDATE_FREQ_NONE, t0, binding = 19);
	RES(Tex2D(float4), normalMaps[MAX_TEXTURE_UNITS],   UPDATE_FREQ_NONE, t1, binding = 19 + MAX_TEXTURE_UNITS);
	RES(Tex2D(float4), specularMaps[MAX_TEXTURE_UNITS], UPDATE_FREQ_NONE, t2, binding = 19 + MAX_TEXTURE_UNITS * 2);
#else
	RES(Tex2D(float4), diffuseMaps[MAX_TEXTURE_UNITS],  space4, t0, binding = 19);
	RES(Tex2D(float4), normalMaps[MAX_TEXTURE_UNITS],   space5, t0, binding = 19 + MAX_TEXTURE_UNITS);
	RES(Tex2D(float4), specularMaps[MAX_TEXTURE_UNITS], space6, t0, binding = 19 + MAX_TEXTURE_UNITS * 2);
#endif

RES(ByteBuffer, vertexPos,           UPDATE_FREQ_NONE, t10, binding=0);
RES(ByteBuffer, vertexTexCoord,      UPDATE_FREQ_NONE, t11, binding=1);
RES(ByteBuffer, vertexNormal,        UPDATE_FREQ_NONE, t12, binding=2);
RES(ByteBuffer, filteredIndexBuffer, UPDATE_FREQ_PER_FRAME, t14, binding=4);

RES(Buffer(InstanceData), instanceData, UPDATE_FREQ_PER_FRAME, t22, binding = 14);
RES(Buffer(uint), indirectInstanceIndexBuffer,       UPDATE_FREQ_PER_FRAME, t15, binding=5);
RES(Buffer(uint), indirectDrawArgs,   UPDATE_FREQ_PER_FRAME, t17, binding=9);
RES(Buffer(MeshConstants), meshConstantsBuffer, UPDATE_FREQ_NONE, t16, binding=6);
RES(Buffer(LightData), lights,                  UPDATE_FREQ_NONE, t19, binding=11);

RES(ByteBuffer, lightClustersCount, UPDATE_FREQ_PER_FRAME, t20, binding=12);
RES(ByteBuffer, lightClusters,      UPDATE_FREQ_PER_FRAME, t21, binding=13);

#if USE_VRS
#if defined(METAL)
	RES(Tex2DMS(uint, SAMPLE_COUNT), historyTex, UPDATE_FREQ_NONE, t3, binding = 13);
#endif
#endif

RES(SamplerState, textureSampler, UPDATE_FREQ_NONE, s0, binding = 7);
RES(SamplerState, depthSampler, UPDATE_FREQ_NONE, s1, binding = 8);

ShadedFragment tri_data_to_frag_color(float4 inPosition, float2 screenPos, uint drawID, uint triangleID, uint geomSet)
{
	// BaseInstanceBuffer returns constant offset values
	// The following value defines the maximum amount of indirect draw calls that will be 
	// drawn at once. This value depends on the number of submeshes or individual objects 
	// in the scene. Changing a scene will require to change this value accordingly.
	// #define MAX_DRAWS_INDIRECT 300 
	//
	// These values are offsets used to point to the material data depending on the 
	// type of geometry and on the culling view
	// #define MATERIAL_BASE_ALPHA0 0
	// #define MATERIAL_BASE_NOALPHA0 MAX_DRAWS_INDIRECT
	// #define MATERIAL_BASE_ALPHA1 (MAX_DRAWS_INDIRECT*2)
	// #define MATERIAL_BASE_NOALPHA1 (MAX_DRAWS_INDIRECT*3)
	uint geomSetBaseSlot = BaseInstanceBuffer(geomSet, VIEW_CAMERA);
	uint drawIndex = geomSetBaseSlot + drawID; // Index into indirect buffers at which data for this specific draw is stored

	// This is the start vertex of the current draw batch
	uint startIndexOffset = INDIRECT_DRAW_ARGUMENTS_START_INDEX_OFFSET;
	uint vertexOffsetOffset = INDIRECT_DRAW_ARGUMENTS_VERTEX_OFFSET_OFFSET;
	// View is always 0, since only CAMERA_VIEW sets are stored
    uint indirectStartElemIndex = GET_INDIRECT_DRAW_ELEM_INDEX(0, geomSet, drawID * INDIRECT_DRAW_ARGUMENTS_STRUCT_NUM_ELEMENTS + startIndexOffset);
	uint vertexOffsetIndex = GET_INDIRECT_DRAW_ELEM_INDEX(0, geomSet, drawID * INDIRECT_DRAW_ARGUMENTS_STRUCT_NUM_ELEMENTS + vertexOffsetOffset);
	uint startIndex = Get(indirectDrawArgs)[indirectStartElemIndex];
	uint drawVertexOffset = Get(indirectDrawArgs)[vertexOffsetIndex]; // vertexOffset written in the indirect draw arguments

	uint triIdx0 = (triangleID * 3 + 0) + startIndex;
	uint triIdx1 = (triangleID * 3 + 1) + startIndex;
	uint triIdx2 = (triangleID * 3 + 2) + startIndex;

	float4x4 mvp = Get(transform)[VIEW_CAMERA].mvp;

	// If this triangle comes from an instance of an object we need to include model matrix
	uint meshIndex = (Get(indirectInstanceIndexBuffer)[drawIndex] & INSTANCE_MESH_MASK) >> INSTANCE_MESH_LOW_BIT;
	uint instanceIndex = (Get(indirectInstanceIndexBuffer)[drawIndex] & INSTANCE_INDEX_MASK) >> INSTANCE_INDEX_LOW_BIT;
	
	// If triangle belongs to an animated instance: drawVertexOffset contains the offset to the animated attributes, 
	// we need a different offset to access the constant data.
	// If triangle is not animated: drawVertexOffset contains the offset to the normal mesh, same as meshConstantsBuffer[meshIndex].vertexOffset
	uint constAttrVertexOffset = drawVertexOffset;
	uint animatedAttrVertexOffset = drawVertexOffset;

	if (instanceIndex != INSTANCE_INDEX_NONE)
	{
		mvp = mul(mvp, Get(instanceData)[instanceIndex].modelMtx);

		uint preSkinnedVtxOffset = Get(instanceData)[instanceIndex].preSkinnedVertexOffset;
		if (preSkinnedVtxOffset != PRE_SKINNED_VERTEX_OFFSET_NONE)
		{
			// Note: drawVertexOffset (used to initialize animatedAttrVertexOffset) should be the same as preSkinnedVtxOffset, 
			//       therefore we don't need thi assignment.
			//animatedAttrVertexOffset = preSkinnedVtxOffset;

			// Read constant attributes from the main mesh data
			constAttrVertexOffset = Get(meshConstantsBuffer)[meshIndex].vertexOffset;
		}
	}

	uint constIndex0 = constAttrVertexOffset + LoadByte(Get(filteredIndexBuffer), triIdx0 << 2);
	uint constIndex1 = constAttrVertexOffset + LoadByte(Get(filteredIndexBuffer), triIdx1 << 2);
	uint constIndex2 = constAttrVertexOffset + LoadByte(Get(filteredIndexBuffer), triIdx2 << 2);

	uint animIndex0 = animatedAttrVertexOffset + LoadByte(Get(filteredIndexBuffer), triIdx0 << 2);
	uint animIndex1 = animatedAttrVertexOffset + LoadByte(Get(filteredIndexBuffer), triIdx1 << 2);
	uint animIndex2 = animatedAttrVertexOffset + LoadByte(Get(filteredIndexBuffer), triIdx2 << 2);

	// Load vertex data of the 3 vertices
	float3 v0pos = asfloat(LoadByte4(Get(vertexPos), animIndex0 * 12)).xyz;
	float3 v1pos = asfloat(LoadByte4(Get(vertexPos), animIndex1 * 12)).xyz;
	float3 v2pos = asfloat(LoadByte4(Get(vertexPos), animIndex2 * 12)).xyz;

	// Transform positions to clip space
	float4 pos0 = mul(mvp, float4(v0pos, 1.0f));
	float4 pos1 = mul(mvp, float4(v1pos, 1.0f));
	float4 pos2 = mul(mvp, float4(v2pos, 1.0f));

	float2 two_over_windowsize = Get(twoOverRes);

	// Compute partial derivatives. This is necessary to interpolate triangle attributes per pixel.
	BarycentricDeriv derivativesOut = CalcFullBary(pos0,pos1,pos2,screenPos, two_over_windowsize);

	// Interpolate the 1/w (one_over_w) for all three vertices of the triangle
	// using the barycentric coordinates and the delta vector
	float w = dot(float3(pos0.w, pos1.w, pos2.w),derivativesOut.m_lambda);

	// Reconstruct the Z value at this screen point performing only the necessary matrix * vector multiplication
	// operations that involve computing Z
	float z = w * getElem(Get(transform)[VIEW_CAMERA].projection, 2, 2) + getElem(Get(transform)[VIEW_CAMERA].projection, 3, 2);

	// Calculate the world position coordinates:
	// First the projected coordinates at this point are calculated using screenPos and the computed Z value at this point.
	// Then, multiplying the perspective projected coordinates by the inverse view-projection matrix (invVP) produces world coordinates
	float3 position = mul(Get(transform)[VIEW_CAMERA].invVP, float4(screenPos * w, z, w)).xyz;

	// TEXTURE COORD INTERPOLATION
	f3x2 texCoords = make_f3x2_cols(
			unpack2Floats(LoadByte(Get(vertexTexCoord), constIndex0 << 2)) ,
			unpack2Floats(LoadByte(Get(vertexTexCoord), constIndex1 << 2)) ,
			unpack2Floats(LoadByte(Get(vertexTexCoord), constIndex2 << 2)) 
	);

	float4 wPos0 = mul(Get(transform)[VIEW_CAMERA].invVP, pos0);
	float4 wPos1 = mul(Get(transform)[VIEW_CAMERA].invVP, pos1);
	float4 wPos2 = mul(Get(transform)[VIEW_CAMERA].invVP, pos2);

#if defined(USE_RAY_DIFFERENTIALS)
	float3 positionDX = mul(Get(transform)[VIEW_CAMERA].invVP, float4((screenPos+two_over_windowsize.x) * w, z, w)).xyz;
	float3 positionDY = mul(Get(transform)[VIEW_CAMERA].invVP, float4((screenPos+two_over_windowsize.y) * w, z, w)).xyz;

	derivativesOut = CalcRayBary(wPos0.xyz,wPos1.xyz,wPos2.xyz,position,positionDX,positionDY,
												Get(camPos).xyz);
#endif

	// potential results for geomSetBaseSlot + drawID are
	// 0 - 299 - shadow alpha
	// 300 - 599 - shadow no alpha
	// 600 - 899 - camera alpha
	uint materialID = Get(meshConstantsBuffer)[meshIndex].materialID;

	// Interpolate texture coordinates and calculate the gradients for texture sampling with mipmapping support
	GradientInterpolationResults results = Interpolate2DWithDeriv(derivativesOut,texCoords);			

	float2 texCoordDX = results.dx;
	float2 texCoordDY = results.dy;
	float2 texCoord = results.interp;

	// CALCULATE PIXEL COLOR USING INTERPOLATED ATTRIBUTES
	// Reconstruct normal map Z from X and Y
	// "NonUniformResourceIndex" is a "pseudo" function see
	// http://asawicki.info/news_1608_direct3d_12_-_watch_out_for_non-uniform_resource_index.html

	// Get textures from arrays.
	float4 normalMapRG;
	float4 diffuseColor;
	float4 specularColor;
	BeginNonUniformResourceIndex(materialID, MAX_TEXTURE_UNITS);
		normalMapRG   = SampleGradTex2D(Get(normalMaps)[materialID],   Get(textureSampler), texCoord, texCoordDX, texCoordDY);
		diffuseColor  = SampleGradTex2D(Get(diffuseMaps)[materialID],  Get(textureSampler), texCoord, texCoordDX, texCoordDY);
		specularColor = SampleGradTex2D(Get(specularMaps)[materialID], Get(textureSampler), texCoord, texCoordDX, texCoordDY);
	EndNonUniformResourceIndex();

	// TheForge UnitTest specific code to test different alpha blend values.
	{
		if (geomSet == GEOMSET_ALPHA_BLEND)
		{
			// We are alpha blending the Flags in SanMiguel, all flags are stored in the same texture, with this we can assign one alpha blend value for each one
			uint transparentIdx = clamp(uint(texCoord.x / 0.25f), 0u, 3u);
			diffuseColor.a *= Get(transAlphaPerFlag)[transparentIdx];
		}
	}

	float3 reconstructedNormalMap;
	reconstructedNormalMap.xy = normalMapRG.ga * 2.0f - 1.0f;
	reconstructedNormalMap.z = sqrt(saturate(1.0f - dot(reconstructedNormalMap.xy, reconstructedNormalMap.xy)));

	// NORMAL INTERPOLATION
	float3x3 normals = make_f3x3_rows(
		decodeDir(unpackUnorm2x16(LoadByte(Get(vertexNormal), animIndex0 << 2))) ,
		decodeDir(unpackUnorm2x16(LoadByte(Get(vertexNormal), animIndex1 << 2))) ,
		decodeDir(unpackUnorm2x16(LoadByte(Get(vertexNormal), animIndex2 << 2)))
	);
	float3 normal = normalize(InterpolateWithDeriv_float3x3(derivativesOut, normals));

	if (instanceIndex != INSTANCE_INDEX_NONE)
	{
		float4x4 modelMtx = Get(instanceData)[instanceIndex].modelMtx;
		normal = normalize(mul(adjoint_float4x4(modelMtx), normal));
	}

	//Calculate pixel normal and tangent vectors
	f3x3 wPositions = make_f3x3_cols(
			wPos0.xyz,
			wPos1.xyz,
			wPos2.xyz
	);

	DerivativesOutput wPosDer = Cal3DDeriv(derivativesOut, wPositions);
	DerivativesOutput uvDer = { float3(results.dx, 0.0),  float3(results.dy, 0.0) };
	normal = perturb_normal(reconstructedNormalMap, normal, wPosDer, uvDer);

	// Sample Diffuse color
	float4 posLS = mul(Get(transform)[VIEW_SHADOW].vp, float4(position, 1.0f));
	
	float Roughness = clamp(specularColor.a, 0.05f, 0.99f);
	float Metallic = specularColor.b;

	float ao = 1.0f;

	bool isTwoSided = (geomSet != GEOMSET_OPAQUE) && bool(Get(meshConstantsBuffer)[meshIndex].flags & MESH_CONSTANT_FLAG_TWO_SIDED);
	bool isBackFace = false;

	float3 ViewVec = normalize(Get(camPos).xyz - position.xyz);
	
	//if it is backface
	//this should be < 0 but our mesh's edge normals are smoothed, badly
	if (isTwoSided && dot(normal, ViewVec) < 0.0f)
	{
		//flip normal
		normal = -normal;
		isBackFace = true;
	}

	float3 HalfVec = normalize(ViewVec - Get(lightDir).xyz);
	float3 ReflectVec = reflect(-ViewVec, normal);
	float NoV = saturate(dot(normal, ViewVec));

	float NoL = dot(normal, -Get(lightDir).xyz);	

	// Deal with two faced materials
	NoL = (isTwoSided ? abs(NoL) : saturate(NoL));

	float3 shadedColor = f3(0.0f);

	// calculate color contribution from specular lighting
	float3 F0 = f3(0.08); // 0.08 is the index of refraction
	float3 SpecularColor = lerp(F0, diffuseColor.rgb, Metallic);
	float3 DiffuseColor = lerp(diffuseColor.rgb, f3(0.0), Metallic);

	float alpha = diffuseColor.a;
	float shadowFactor = 1.0f;
	float fLightingMode = saturate(float(Get(lightingMode)));

	shadedColor = calculateIllumination(
		    normal,
		    ViewVec,
			HalfVec,
			ReflectVec,
			NoL,
			NoV,
			Get(camPos).xyz,
			Get(esmControl),
			Get(lightDir).xyz,
			posLS,
			position,
			Get(shadowMap),
			DiffuseColor,
			SpecularColor,
			Roughness,
			Metallic,			
			Get(depthSampler),
			isBackFace,
			fLightingMode,
			shadowFactor);
			
	shadedColor = shadedColor * Get(lightColor).rgb * Get(lightColor).a * NoL;
	
	// point lights
	// Find the light cluster for the current pixel
	uint2 clusterCoords = uint2(floor((screenPos * 0.5f + 0.5f) * float2(LIGHT_CLUSTER_WIDTH, LIGHT_CLUSTER_HEIGHT)));

	uint numLightsInCluster = LoadByte(Get(lightClustersCount), LIGHT_CLUSTER_COUNT_POS(clusterCoords.x, clusterCoords.y) << 2);

	// Accumulate light contributions
	for (uint j = 0; j < numLightsInCluster; ++j)
	{
		uint lightId = LoadByte(Get(lightClusters), LIGHT_CLUSTER_DATA_POS(j, clusterCoords.x, clusterCoords.y) << 2);

		shadedColor += pointLightShade(
		normal,
		ViewVec,
		HalfVec,
		ReflectVec,
		NoL,
		NoV,
		Get(lights)[lightId].position.xyz,
		Get(lights)[lightId].color.xyz,
		Get(camPos).xyz,
		Get(lightDir).xyz,
		posLS,
		position,
		DiffuseColor,
		SpecularColor,
		Roughness,
		Metallic,		
		isBackFace,
		fLightingMode);
	}

	float ambientIntencity = 0.05f * ao;
	float3 ambient = diffuseColor.rgb * ambientIntencity;

	ShadedFragment shadedFragment;
	shadedFragment.color = float4(shadedColor + ambient, alpha);
	shadedFragment.depth = z / w;
	return shadedFragment;
}

float4 PS_MAIN( VSOutput In, SV_SampleIndex(uint) i )
{
	INIT_MAIN;
#if USE_VRS
#if defined(METAL)
	float4 result = float4(0, 0, 0, 0);
	uint rate = GetConservativeRate(LoadTex2DMS(Get(historyTex), NO_SAMPLER, uint2(In.position.xy), 0).r);
	if (rate == SHADING_RATE_2X2 && (i != 0))
		RETURN(result);
	if (rate == SHADING_RATE_1X2 && ((i == 1) || (i == 3)))
		RETURN(result);
	if (rate == SHADING_RATE_2X1 && ((i == 2) || (i == 3)))
		RETURN(result);
#endif
#endif

	// Load Visibility Buffer raw packed float4 data from render target
#if(SAMPLE_COUNT > 1)
	float4 visRaw = LoadTex2DMS(Get(vbTex), Get(depthSampler), uint2(In.position.xy), i);
#else
	float4 visRaw = LoadTex2D(Get(vbTex), Get(depthSampler), uint2(In.position.xy), 0);
#endif
	// Unpack float4 render target data into uint to extract data
	uint alphaBit_transBit_drawID_triID = packUnorm4x8(visRaw);

	uint opaqueShaded = 0;
	uint transparentShaded = 0; 

	float VisDepth = 0.0f;
	float3 OutColor = float3(0, 0, 0);
#if USE_VRS
	float2 step = float2(2.f/Get(screenWidth), 2.f/Get(screenHeight));
	float2 screenPos = clamp(In.screenPos + step * samplePositions[i], -1.f, 1.f);
#endif
	// Early exit if this pixel doesn't contain triangle data
	if (alphaBit_transBit_drawID_triID != ~0u)
	{
		// Extract packed data
		uint drawID = (alphaBit_transBit_drawID_triID & DRAW_ID_MASK) >> DRAW_ID_LOW_BIT;
		uint triangleID = (alphaBit_transBit_drawID_triID & PRIM_ID_MASK) >> PRIM_ID_LOW_BIT;
		uint alpha1_opaque0 = (alphaBit_transBit_drawID_triID & ALPH_IS_MASK) >> ALPH_IS_LOW_BIT;
		uint geomSet = alpha1_opaque0; // GEOMSET_OPAQUE == 0, GEOMSET_ALPHA_CUTOUT == 1

#if USE_VRS
		ShadedFragment shadedFragment = tri_data_to_frag_color(In.position, screenPos, drawID, triangleID, geomSet);
#else
		ShadedFragment shadedFragment = tri_data_to_frag_color(In.position, In.screenPos, drawID, triangleID, geomSet);
#endif
		OutColor = shadedFragment.color.rgb; // Alpha is 1 for GEOMSET_OPAQUE and GEOMSET_ALPHA_CUTOUT, we can ignore it
		VisDepth = shadedFragment.depth;

		opaqueShaded = 1; 
	}

#if USE_VRS
	uint2 pixelAddr = uint2(In.position.xy) * 2 + sampleOffsets[i];
#else
	uint2 pixelAddr = uint2(In.position.xy);
#endif

	uint scrW = Get(screenWidth); 
	uint scrH = Get(screenHeight); 
	uint nodeIdx = OIT_HEAD_INVALID;
	if (pixelAddr.x < scrW && pixelAddr.y < scrH)
	{
		uint bufferIdx = pixelAddr.y * scrW + pixelAddr.x;
		nodeIdx = Get(headIndexBufferSRV)[bufferIdx];
	}

	if(nodeIdx == OIT_HEAD_INVALID && opaqueShaded == 0u)
	{
		discard;
	}

	float transAlpha = 0.f;
	if (nodeIdx != OIT_HEAD_INVALID)
	{
		uint count = 0; 
		NodeFinalOIT fragments[OIT_MAX_FRAG_COUNT];

		// Accumulate transparent pixel color data
		LOOP for (uint loopIteration = 0;
					loopIteration < OIT_MAX_FRAG_COUNT && nodeIdx != OIT_HEAD_INVALID;
					++loopIteration)
		{
			TransparentNodeOIT node = Get(vbDepthLinkedListSRV)[nodeIdx];

			uint nodeNextIdx = node.next;
			uint nodeTriangleData = node.triangleData;

			uint nodeDrawID = (nodeTriangleData & DRAW_ID_MASK) >> DRAW_ID_LOW_BIT;
			uint nodeTriangleID = (nodeTriangleData & PRIM_ID_MASK) >> PRIM_ID_LOW_BIT;
#if USE_VRS
			ShadedFragment nodeColorData = tri_data_to_frag_color(In.position, screenPos, nodeDrawID, nodeTriangleID, GEOMSET_ALPHA_BLEND);
#else
			ShadedFragment nodeColorData = tri_data_to_frag_color(In.position, In.screenPos, nodeDrawID, nodeTriangleID, GEOMSET_ALPHA_BLEND);
#endif

			// Manual visbuf depth test
			if (nodeColorData.depth > VisDepth)
			{
				fragments[count].color = nodeColorData.color.xyz; 
				fragments[count].alpha = nodeColorData.color.a;
				fragments[count].depth = nodeColorData.depth;
				fragments[count].next  = nodeNextIdx;

				++count;
			}

			nodeIdx = nodeNextIdx;
		}

		// May be no fragments left after manual depth cull
		if (count > 0)
		{
			// Insertion sort
			for (uint j = 1; j < count; ++j)
			{
				NodeFinalOIT insert = fragments[j];
				uint k = j;
			
				while (k > 0)
				{
					if (insert.depth >= fragments[k - 1].depth)
					{
						break; 
					}
					
					fragments[k] = fragments[k - 1];
					--k;
				}
			
				fragments[k] = insert; 
			}

			// Blending
			for (uint l = 0; l < count; ++l)
			{
				OutColor = lerp(OutColor, fragments[l].color, fragments[l].alpha);
				transAlpha = saturate(transAlpha * (1.f - fragments[l].alpha) + fragments[l].alpha);
			}

			transparentShaded = 1; 
		}
	}

	float OutAlpha = (transparentShaded == 1 && opaqueShaded == 0) ? transAlpha : 1.0f; 
	RETURN(float4(OutColor, OutAlpha));
}
