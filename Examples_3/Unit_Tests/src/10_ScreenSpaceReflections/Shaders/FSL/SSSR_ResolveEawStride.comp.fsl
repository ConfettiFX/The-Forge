/**********************************************************************
Copyright (c) 2020 Advanced Micro Devices, Inc. All rights reserved.

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in
all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.  IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
THE SOFTWARE.
********************************************************************/

/*
 * Copyright (c) 2017-2022 The Forge Interactive Inc.
 * 
 * This file is part of The-Forge
 * (see https://github.com/ConfettiFX/The-Forge).
 * 
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 * 
 *   http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
*/

#ifndef FFX_SSSR_EAW_RESOLVE
#define FFX_SSSR_EAW_RESOLVE

RES(Tex2D(float4), g_normal, UPDATE_FREQ_NONE, t0, binding = 5);
RES(Tex2D(float4), g_roughness, UPDATE_FREQ_NONE, t1, binding = 2);
RES(Tex2D(float), g_depth_buffer, UPDATE_FREQ_NONE, t2, binding = 6);

RES(RWTex2D(float4), g_temporally_denoised_reflections, UPDATE_FREQ_NONE, u0, binding = 1);
RES(RWTex2D(float4), g_denoised_reflections, UPDATE_FREQ_NONE, u1, binding = 3);
RES(RWBuffer(uint), g_tile_list, UPDATE_FREQ_NONE, u2, binding = 4);

#include "SSSR_Common.h.fsl"

min16float3 LoadRadiance(int2 idx)
{
    return min16float3(LoadRWTex2D(Get(g_temporally_denoised_reflections), idx).xyz);
}

min16float LoadRoughnessValue(int2 idx)
{
    return min16float(FfxSssrUnpackRoughness(LoadTex2D(Get(g_roughness), NO_SAMPLER, idx, 0)));
}

min16float GetRoughnessRadiusWeight(min16float roughness_p, min16float roughness_q, min16float dist)
{
    return 1.0 - smoothstep(10 * roughness_p, 500 * roughness_p, dist);
}

min16float4 ResolveScreenspaceReflections(int2 did, min16float center_roughness)
{
    const min16float roughness_sigma_min = 0.001;
    const min16float roughness_sigma_max = 0.01;

    min16float3 sum = min16float3(f3(0.0));
    min16float total_weight = 0.0;

    const int radius = 2;
    for (int dy = -radius; dy <= radius; ++dy)
    {
        for (int dx = -radius; dx <= radius; ++dx)
        {
            int2 texel_coords = did + FFX_SSSR_EAW_STRIDE * int2(dx, dy);

            min16float3 radiance = LoadRadiance(texel_coords);
            min16float roughness = LoadRoughnessValue(texel_coords);

            min16float dist = min16float(length(float2(texel_coords - did)));
            min16float weight = GetEdgeStoppingRoughnessWeightFP16(center_roughness, roughness, roughness_sigma_min, roughness_sigma_max)
                * GetRoughnessRadiusWeight(center_roughness, roughness, dist);
            sum += weight * radiance;
            total_weight += weight;
        }
    }

    sum /= fast_max(total_weight, min16float(0.0001));
    return min16float4(sum, 1);
}

void Resolve(int2 did)
{
    // min16float3 center_radiance = LoadRadiance(did);
    min16float center_roughness = LoadRoughnessValue(did);
    if (!IsGlossy(center_roughness) || IsMirrorReflection(center_roughness))
    {
        return;
    }
    Write2D(Get(g_denoised_reflections), did, float4(ResolveScreenspaceReflections(did, center_roughness)));
}

NUM_THREADS(8,  8,  1)
void CS_MAIN( SV_GroupThreadID(uint3) group_thread_id, SV_GroupID(uint3) group_id )
{
	INIT_MAIN;
    uint packed_base_coords = Get(g_tile_list)[group_id.x];
    uint2 base_coords = Unpack(packed_base_coords);
    uint2 coords = base_coords + group_thread_id.xy;
    Resolve(int2(coords));
	RETURN();
}
#endif // FFX_SSSR_EAW_RESOLVE