/*
 * Copyright (c) 2017-2022 The Forge Interactive Inc.
 * 
 * This file is part of The-Forge
 * (see https://github.com/ConfettiFX/The-Forge).
 * 
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 * 
 *   http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
*/

#define NUM_SAMPLE 1
#define MAX_PLANES 4

#define PI 3.1415926535897932384626422832795028841971
#define RADIAN 0.01745329251994329576923690768489

CBUFFER(cbExtendCamera, UPDATE_FREQ_PER_FRAME, b0, binding = 0)
{
	DATA(float4x4, viewMat, None);
	DATA(float4x4, projMat, None);
	DATA(float4x4, viewProjMat, None);
	DATA(float4x4, InvViewProjMat, None);

	DATA(float4, cameraWorldPos, None);
	DATA(float4, viewPortSize, None);
};


RES(Tex2D(float4), SceneTexture, UPDATE_FREQ_NONE, t1, binding = 1);
RES(RWBuffer(uint), IntermediateBuffer, UPDATE_FREQ_NONE, u2, binding = 2);

RES(Tex2D(float4), DepthTexture, UPDATE_FREQ_NONE, t3, binding = 3);

STRUCT(PlaneInfo)
{
	DATA(float4x4, rotMat, None);
	DATA(float4, centerPoint, None);
	DATA(float4, size, None);
};

CBUFFER(planeInfoBuffer, UPDATE_FREQ_PER_FRAME, b4, binding = 4)
{
	DATA(PlaneInfo, planeInfo[MAX_PLANES], None);
	DATA(uint, numPlanes, None);
	DATA(uint, pad00, None);
	DATA(uint, pad01, None);
	DATA(uint, pad02, None);
};

CBUFFER(cbProperties, UPDATE_FREQ_PER_FRAME, b5, binding = 5)
{
	DATA(uint, renderMode, None);
	DATA(float, useHolePatching, None);
	DATA(float, useExpensiveHolePatching, None);
	DATA(float, useNormalMap, None);

	DATA(float, intensity, None);
	DATA(float, useFadeEffect, None);
	DATA(float, padding01, None);
	DATA(float, padding02, None);
};

RES(SamplerState, defaultSampler, UPDATE_FREQ_NONE, s6, binding = 6);


bool intersectPlane(in(uint) index, in(float3) worldPos, in(float2) fragUV, out(float4) hitPos, out(float2) relfectedUVonPlanar) 
{

	PlaneInfo thisPlane = Get(planeInfo)[index];

	// assuming vectors are all normalized
	//float4x4 thisPlanesMat = thisPlane.rotMat;
	float3 normalVec = getCol2(thisPlane.rotMat).xyz;

	float3 centerPoint = thisPlane.centerPoint.xyz;

	float3 rO = Get(cameraWorldPos).xyz;
	float3 rD = normalize(worldPos - rO);
	float3 rD_VS = mul(to_f3x3(Get(viewMat)), rD);
	

	//hitPos = float4(  normalVec, 0.0);

	if(rD_VS.z < 0.0)
		return false;	

    float denom = dot(normalVec, rD); 

    if (denom < 0.0)
	{ 
		

        float3 p0l0 = centerPoint - rO; 
        float t = dot(normalVec, p0l0) / denom; 

		if(t <= 0.0)
			return false;

		float3 hitPoint = rO + rD*t;	

		float3 gap = hitPoint - centerPoint;
		
		float xGap = dot(gap, getCol0(thisPlane.rotMat).xyz);
		float yGap = dot(gap, getCol1(thisPlane.rotMat).xyz);


		float width = thisPlane.size.x * 0.5;
		float height = thisPlane.size.y * 0.5;

		float4 reflectedPos;

		if( (abs(xGap) <= width) && (abs(yGap) <= height))
		{
			hitPos = float4(hitPoint, 1.0);
			reflectedPos = mul(Get(viewProjMat), hitPos);
			reflectedPos /= reflectedPos.w;

			reflectedPos.xy = float2( (reflectedPos.x + 1.0) * 0.5, (1.0 - reflectedPos.y) * 0.5);

			float depth = SampleTex2D(Get(DepthTexture), Get(defaultSampler), reflectedPos.xy).r;

			if(depth <= reflectedPos.z)
				return false;
			
			if( reflectedPos.x < 0.0 || reflectedPos.y < 0.0  || reflectedPos.x > 1.0 || reflectedPos.y > 1.0 )
				return false;
			else
			{
				relfectedUVonPlanar = float2(xGap / width, yGap / height) * 0.5 + float2(0.5, 0.5);
				relfectedUVonPlanar *= float2(thisPlane.size.zw);

				return true; 
			}			
		}	
		else
			return false;
    } 
	else	
		return false; 
} 

float4 unPacked(in(uint) unpacedInfo, in(float2) dividedViewSize, out(uint) CoordSys)
{
	float YInt = float(unpacedInfo >> 20);
	int YFrac = int( (unpacedInfo & 0x000E0000) >> 17 );
	
	uint uXInt = (unpacedInfo & 0x00010000) >> 16;

	float XInt = 0.0;

	if(uXInt == 0)
	{
		XInt = float( int(  (unpacedInfo & 0x0001FFE0) >> 5 ));
	}
	else
	{
		XInt = float(int( ((unpacedInfo & 0x0001FFE0) >> 5) | 0xFFFFF000));
	}
	
	int XFrac = int( (unpacedInfo & 0x0000001C) >> 2 );

	float Yfrac = YFrac * 0.125;
	float Xfrac = XFrac * 0.125;
	
	CoordSys = unpacedInfo & 0x00000003;

	float2 offset = float2(0.0, 0.0);

	if(CoordSys == 0)
	{
		offset = float2( (XInt) / dividedViewSize.x, (YInt)  / dividedViewSize.y);
		//offset = float2(XInt, YInt);
	}
	else if(CoordSys == 1)
	{
		offset = float2( (YInt) / dividedViewSize.x, (XInt) / dividedViewSize.y);
		//offset = float2(0.0, 1.0);
	}
	else if(CoordSys == 2)
	{
		offset = float2( (XInt) / dividedViewSize.x, -(YInt) / dividedViewSize.y);
		//offset = float2(0.5, 0.5);
	}
	else if(CoordSys == 3)
	{
		offset = float2( -(YInt) / dividedViewSize.x, (XInt) / dividedViewSize.y);
		//offset = float2(1.0, 1.0);
	}

	return float4(offset, Xfrac, Yfrac);
}

float4 getWorldPosition(float2 UV, float depth)
{
	float4 worldPos = mul( Get(InvViewProjMat), float4(UV.x*2.0 - 1.0, (1.0 - UV.y) * 2.0 - 1.0, depth, 1.0));
	worldPos /= worldPos.w;

	return worldPos;
}

float fade(float2 UV)
{
	float2 NDC = UV * 2.0 - float2(1.0, 1.0);

	return clamp( 1.0 - max( pow( NDC.y * NDC.y, 4.0) , pow( NDC.x * NDC.x, 4.0)) , 0.0, 1.0); 
}

STRUCT(VSOutput)
{
	DATA(float4, Position, SV_Position);
	DATA(float2, uv, TEXCOORD0);
};


float4 PS_MAIN( VSOutput In )
{	
	INIT_MAIN;
	float4 outColor;
	outColor =  float4(0.0, 0.0, 0.0, 0.0);
	
	uint screenWidth = uint( Get(viewPortSize).x );
	// uint screenHeight = uint( Get(viewPortSize).y );
	
	uint indexY = uint( In.uv.y * Get(viewPortSize).y );
	uint indexX = uint( In.uv.x * Get(viewPortSize).x );
	
	uint index = indexY * screenWidth +  indexX;

	uint bufferInfo = Get(IntermediateBuffer)[index];	
	//InterlockedAdd(Get(IntermediateBuffer)[index], 0, bufferInfo);

	bool bIsInterect = false;

	uint CoordSys;
	float2 offset = unPacked( bufferInfo , Get(viewPortSize).xy, CoordSys).xy;

	float depth =  SampleTex2D(Get(DepthTexture), Get(defaultSampler), In.uv).r;

	float4 worldPos = getWorldPosition(In.uv, depth);

	float4 HitPos_WS;
	float2 UVforNormalMap = float2(0.0, 0.0);

	float4 minHitPos_WS;
	float2 minUVforNormalMap = UVforNormalMap;

	float minDist = 1000000.0;

	//Check if current pixel is in the bound of planars
	for(uint i = 0; i < Get(numPlanes); i++)
	{	
		if(intersectPlane( i, worldPos.xyz, In.uv, HitPos_WS, UVforNormalMap))
		{
			float localDist =  distance(HitPos_WS.xyz, Get(cameraWorldPos).xyz);
			if( localDist <  minDist )
			{
				minDist = localDist;
				minHitPos_WS = HitPos_WS;
				minUVforNormalMap = UVforNormalMap;
			}
			bIsInterect = true;
		}
	}
	

	//If is not in the boundary of planar, exit
	if(!bIsInterect)
	{
		//clear IntermediateBuffer
		AtomicMax(Get(IntermediateBuffer)[index] , UINT_MAX);
		RETURN(outColor);
	}	
		

	float2 relfectedUV = In.uv + offset.xy;

	float offsetLen = FLT_MAX;

	if( bufferInfo < UINT_MAX)
	{
		//values correction
		float correctionPixel = 1.0;

		if(CoordSys == 0)
			relfectedUV = relfectedUV.xy + float2(0.0, correctionPixel/Get(viewPortSize).y);
		else if(CoordSys == 1)
			relfectedUV = relfectedUV.xy + float2(correctionPixel/Get(viewPortSize).x, 0.0);
		else if(CoordSys == 2)
			relfectedUV = relfectedUV.xy - float2(0.0, correctionPixel/Get(viewPortSize).y);
		else if(CoordSys == 3)
			relfectedUV = relfectedUV.xy - float2(correctionPixel/Get(viewPortSize).x, 0.0);

		offsetLen = length(offset.xy);
	
		outColor = SampleLvlTex2D(Get(SceneTexture), Get(defaultSampler), relfectedUV, 0);
		
		if(Get(useFadeEffect) > 0.5 )
			outColor *= fade(relfectedUV);
	
		outColor.w = offsetLen;	
		
	}
	else
	{
		outColor.w = FLT_MAX;
	}

	//clear IntermediateBuffer
	AtomicMax(Get(IntermediateBuffer)[ index ] , UINT_MAX);
	RETURN(outColor);
}
