/*
 * Copyright (c) 2017-2022 The Forge Interactive Inc.
 * 
 * This file is part of The-Forge
 * (see https://github.com/ConfettiFX/The-Forge).
 * 
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 * 
 *   http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
*/

PUSH_CONSTANT(RootConstant, b0)
{
	DATA(uint, mip, None);
	DATA(uint, maxSize, None);
};
RES(Tex2D(float4), srcTexture, UPDATE_FREQ_NONE, t1, binding = 1);
RES(WTex2DArray(float4), dstTexture, UPDATE_FREQ_PER_DRAW, u2, binding = 3);
RES(SamplerState, skyboxSampler, UPDATE_FREQ_NONE, s3, binding = 2);

NUM_THREADS(16,  16,  1)
void CS_MAIN( SV_DispatchThreadID(uint3) DTid) 
{
	INIT_MAIN;
	float2 invAtan = float2(0.1591f, 0.3183f);

	float3 threadPos = float3(DTid);

	{
		uint mipSize = Get(maxSize) >> Get(mip);
		if (threadPos.x >= mipSize || threadPos.y >= mipSize)
			RETURN();

		float2 texcoords = float2(float(threadPos.x + 0.5) / mipSize, 1.0f - float(threadPos.y + 0.5) / mipSize);
		float3 sphereDir = f3(0.0f);
		if (threadPos.z <= 0)
			sphereDir = normalize(float3(0.5, -(texcoords.y - 0.5), -(texcoords.x - 0.5)));
		else if (threadPos.z <= 1)
			sphereDir = normalize(float3(-0.5, -(texcoords.y - 0.5), texcoords.x - 0.5));
		else if (threadPos.z <= 2)
			sphereDir = normalize(float3(texcoords.x - 0.5, -0.5, -(texcoords.y - 0.5)));
		else if (threadPos.z <= 3)
			sphereDir = normalize(float3(texcoords.x - 0.5, 0.5, texcoords.y - 0.5));
		else if (threadPos.z <= 4)
			sphereDir = normalize(float3(texcoords.x - 0.5, -(texcoords.y - 0.5), 0.5));
		else if (threadPos.z <= 5)
			sphereDir = normalize(float3(-(texcoords.x - 0.5), -(texcoords.y - 0.5), -0.5));

		float2 panoUVs = float2(atan2(sphereDir.z, sphereDir.x), asin(sphereDir.y));
		panoUVs *= invAtan;
		panoUVs += 0.5f;

		// We need to use SampleLevel, since compute shaders do not support regular sampling 
		Write3D(Get(dstTexture), int3(threadPos), SampleLvlTex2D(Get(srcTexture), Get(skyboxSampler), panoUVs, float(Get(mip))));
	}
	RETURN();
}
