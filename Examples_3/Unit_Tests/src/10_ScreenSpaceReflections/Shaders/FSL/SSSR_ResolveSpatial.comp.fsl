/**********************************************************************
Copyright (c) 2020 Advanced Micro Devices, Inc. All rights reserved.

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in
all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.  IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
THE SOFTWARE.
********************************************************************/

/*
 * Copyright (c) 2017-2022 The Forge Interactive Inc.
 * 
 * This file is part of The-Forge
 * (see https://github.com/ConfettiFX/The-Forge).
 * 
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 * 
 *   http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
*/

#ifndef FFX_SSSR_SPATIAL_RESOLVE
#define FFX_SSSR_SPATIAL_RESOLVE

RES(Tex2D(float), g_depth_buffer, UPDATE_FREQ_NONE, t0, binding = 4);
RES(Tex2D(float4), g_normal, UPDATE_FREQ_NONE, t1, binding = 3);
RES(Tex2D(float4), g_roughness, UPDATE_FREQ_NONE, t2, binding = 6);

// RES(SamplerState, g_linear_sampler, UPDATE_FREQ_NONE, s0, binding = binding = _INSERT_);

RES(RWTex2D(float4), g_spatially_denoised_reflections, UPDATE_FREQ_NONE, u0, binding = 7);
RES(RWTex2D(float), g_ray_lengths, UPDATE_FREQ_NONE, u1, binding = 1);
RES(RWTex2D(float4), g_intersection_result, UPDATE_FREQ_NONE, u2, binding = 2);
RES(RWTex2D(float), g_has_ray, UPDATE_FREQ_NONE, u3, binding = 5);
RES(RWBuffer(uint), g_tile_list, UPDATE_FREQ_NONE, u4, binding = 8);

#include "SSSR_Common.h.fsl"


// Only really need 16x16 but 17x17 avoids bank conflicts.
GroupShared(uint,  g_shared_0[17][17]);
GroupShared(uint,  g_shared_1[17][17]);
GroupShared(uint,  g_shared_2[17][17]);
GroupShared(uint,  g_shared_3[17][17]);
GroupShared(float, g_shared_depth[17][17]);

half4 LoadRadianceFromGroupSharedMemory(int2 idx)
{
    uint2 tmp;
    tmp.x = Get(g_shared_0)[idx.x][idx.y];
    tmp.y = Get(g_shared_1)[idx.x][idx.y];
    return half4(UnpackFloat16(tmp.x), UnpackFloat16(tmp.y));
}

half3 LoadNormalFromGroupSharedMemory(int2 idx)
{
    uint2 tmp;
    tmp.x = Get(g_shared_2)[idx.x][idx.y];
    tmp.y = Get(g_shared_3)[idx.x][idx.y];
    return half4(UnpackFloat16(tmp.x), UnpackFloat16(tmp.y)).xyz;
}

float LoadDepthFromGroupSharedMemory(int2 idx)
{
    return Get(g_shared_depth)[idx.x][idx.y];
}

void StoreInGroupSharedMemory(int2 idx, min16float4 radiance, min16float3 normal, float depth)
{
    Get(g_shared_0)[idx.x][idx.y] = PackFloat16(min16float2(radiance.xy));
    Get(g_shared_1)[idx.x][idx.y] = PackFloat16(min16float2(radiance.zw));
    Get(g_shared_2)[idx.x][idx.y] = PackFloat16(min16float2(normal.xy));
    Get(g_shared_3)[idx.x][idx.y] = PackFloat16(min16float2(normal.z, 0));
    Get(g_shared_depth)[idx.x][idx.y] = depth;
}

min16float LoadRayLengthFP16(int2 idx)
{
    return min16float(LoadRWTex2D(Get(g_ray_lengths), idx).x);
}

min16float3 LoadRadianceFP16(int2 idx)
{
    return min16float3(LoadRWTex2D(Get(g_intersection_result), idx).xyz);
}

min16float3 LoadNormalFP16(int2 idx)
{
    return min16float3(FfxSssrUnpackNormals(LoadTex2D(Get(g_normal), NO_SAMPLER, idx, 0)));
}

float LoadDepth(int2 idx)
{
    return FfxSssrUnpackDepth(LoadTex2D(Get(g_depth_buffer), NO_SAMPLER, idx, 0).x);
}

bool LoadHasRay(int2 idx)
{
    return LoadRWTex2D(Get(g_has_ray), idx).x != 0.0f;
}

void LoadWithOffset(int2 did, int2 offset, out(min16float) ray_length, out(min16float3) radiance, out(min16float3) normal, out(float) depth, out(bool) has_ray)
{
    did += offset;
    ray_length = LoadRayLengthFP16(did);
    radiance = LoadRadianceFP16(did);
    normal = LoadNormalFP16(did);
    depth = LoadDepth(did);
    has_ray = LoadHasRay(did);
}

void StoreWithOffset(int2 gtid, int2 offset, min16float ray_length, min16float3 radiance, min16float3 normal, float depth)
{
    gtid += offset;
    StoreInGroupSharedMemory(gtid, min16float4(radiance, ray_length), normal, depth); // Pack ray length and radiance together
}

void InitializeGroupSharedMemory(int2 did, int2 gtid)
{
    const uint samples_per_quad = Get(g_samples_per_quad);

    // First pass, load (1 + 3 + 8 + 3 + 1) = (16x16) region into shared memory.
    // That is a guard band of 3, the inner region of 8 plus one additional band to catch base pixels if we didn't shoot rays for the respective edges/corners of the loaded region.

    int2 offset_0 = i2(0);
    int2 offset_1 = int2(8, 0);
    int2 offset_2 = int2(0, 8);
    int2 offset_3 = int2(8, 8);

    min16float ray_length_0;
    min16float3 radiance_0;
    min16float3 normal_0;
    float depth_0;
    bool has_ray_0;

    min16float ray_length_1;
    min16float3 radiance_1;
    min16float3 normal_1;
    float depth_1;
    bool has_ray_1;

    min16float ray_length_2;
    min16float3 radiance_2;
    min16float3 normal_2;
    float depth_2;
    bool has_ray_2;

    min16float ray_length_3;
    min16float3 radiance_3;
    min16float3 normal_3;
    float depth_3;
    bool has_ray_3;

    /// XA
    /// BC

    did -= 4; // 1 + 3 => additional band + left band
    LoadWithOffset(did, offset_0, ray_length_0, radiance_0, normal_0, depth_0, has_ray_0); // X
    LoadWithOffset(did, offset_1, ray_length_1, radiance_1, normal_1, depth_1, has_ray_1); // A
    LoadWithOffset(did, offset_2, ray_length_2, radiance_2, normal_2, depth_2, has_ray_2); // B
    LoadWithOffset(did, offset_3, ray_length_3, radiance_3, normal_3, depth_3, has_ray_3); // C

    // If own values are invalid, because no ray created them, lookup the values from the neighboring threads
    const uint lane_index = WaveGetLaneIndex();
    const uint base_lane_index = GetBaseLane(lane_index, samples_per_quad); // As offsets are multiples of 8, we always get the same base lane index no matter the offset.
    const bool is_base_ray = base_lane_index == lane_index;

    const uint lane_index_0 = (has_ray_0 || is_base_ray) ? lane_index : base_lane_index;
    const uint lane_index_1 = (has_ray_1 || is_base_ray) ? lane_index : base_lane_index;
    const uint lane_index_2 = (has_ray_2 || is_base_ray) ? lane_index : base_lane_index;
    const uint lane_index_3 = (has_ray_3 || is_base_ray) ? lane_index : base_lane_index;

    radiance_0 = WaveReadLaneAt(radiance_0, lane_index_0);
    radiance_1 = WaveReadLaneAt(radiance_1, lane_index_1);
    radiance_2 = WaveReadLaneAt(radiance_2, lane_index_2);
    radiance_3 = WaveReadLaneAt(radiance_3, lane_index_3);

    ray_length_0 = WaveReadLaneAt(ray_length_0, lane_index_0);
    ray_length_1 = WaveReadLaneAt(ray_length_1, lane_index_1);
    ray_length_2 = WaveReadLaneAt(ray_length_2, lane_index_2);
    ray_length_3 = WaveReadLaneAt(ray_length_3, lane_index_3);

    StoreWithOffset(gtid, offset_0, ray_length_0, radiance_0, normal_0, depth_0); // X
    StoreWithOffset(gtid, offset_1, ray_length_1, radiance_1, normal_1, depth_1); // A
    StoreWithOffset(gtid, offset_2, ray_length_2, radiance_2, normal_2, depth_2); // B
    StoreWithOffset(gtid, offset_3, ray_length_3, radiance_3, normal_3, depth_3); // C
}

min16float3 ResolveScreenspaceReflections(int2 gtid, min16float3 center_radiance, min16float3 center_normal, float center_depth)
{
    float3 accumulated_radiance = float3(center_radiance);
    float accumulated_weight = 1;

    const float normal_sigma = 64.0;
    const float depth_sigma = 0.02;

    // First 15 numbers of Halton(2,3) streteched to [-3,3]
    const int2 reuse_offsets[] = {
        int2(0, 1),
        int2(-2, 1),
        int2(2, -3),
        int2(-3, 0),
        int2(1, 2),
        int2(-1, -2),
        int2(3, 0),
        int2(-3, 3),
        int2(0, -3),
        int2(-1, -1),
        int2(2, 1),
        int2(-2, -2),
        int2(1, 0),
        int2(0, 2),
        int2(3, -1)
    };
    const uint sample_count = 15;

    for (uint i = 0; i < sample_count; ++i)
    {
        int2 new_idx = gtid + reuse_offsets[i];
        half3 normal = LoadNormalFromGroupSharedMemory(new_idx);
        float depth = LoadDepthFromGroupSharedMemory(new_idx);
        half4 radiance = LoadRadianceFromGroupSharedMemory(new_idx);
        float weight = 1
            * GetEdgeStoppingNormalWeight(float3(center_normal), float3(normal), normal_sigma)
            * Gaussian(center_depth, depth, depth_sigma)
            ;

        // Accumulate all contributions.
        accumulated_weight += weight;
        accumulated_radiance += float3(weight * radiance.xyz);
    }

    accumulated_radiance /= max(accumulated_weight, 0.00001);
    return min16float3(accumulated_radiance);
}

void Resolve(int2 did, int2 gtid)
{
    float center_roughness = LoadRoughness(did, Get(g_roughness));
    InitializeGroupSharedMemory(did, gtid);
    GroupMemoryBarrier();

    if (!IsGlossy(center_roughness) || IsMirrorReflection(center_roughness))
    {
        return;
    }

    gtid += 4; // Center threads in groupshared memory

    min16float4 center_radiance = min16float4(LoadRadianceFromGroupSharedMemory(gtid));
    min16float3 center_normal   = min16float3(LoadNormalFromGroupSharedMemory(gtid));
    float  center_depth    = LoadDepthFromGroupSharedMemory(gtid);
    min16float3 radiance = ResolveScreenspaceReflections(gtid, center_radiance.xyz, center_normal, center_depth);
    Write2D(Get(g_spatially_denoised_reflections), did.xy, float4(float3(radiance.xyz), 1.0f));
    Write2D(Get(g_ray_lengths), did.xy, float(center_radiance.w)); // ray_length
}

NUM_THREADS(64,  1,  1)
void CS_MAIN( SV_GroupThreadID(uint3) group_thread_id_linear, SV_GroupID(uint3) group_id )
{
	INIT_MAIN;
    uint packed_base_coords = Get(g_tile_list)[group_id.x];
    uint2 base_coords = Unpack(packed_base_coords);
    uint2 group_thread_id_2d = RemapLane8x8(group_thread_id_linear.x);
    uint2 coords = base_coords + group_thread_id_2d;
    Resolve(int2(coords), int2(group_thread_id_2d));
	RETURN();
}
#endif // FFX_SSSR_SPATIAL_RESOLVE