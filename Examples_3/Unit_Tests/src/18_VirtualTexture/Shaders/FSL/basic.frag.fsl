/*
 * Copyright (c) 2017-2022 The Forge Interactive Inc.
 * 
 * This file is part of The-Forge
 * (see https://github.com/ConfettiFX/The-Forge).
 * 
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 * 
 *   http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
*/

#include "resources.h.fsl"

RES(Tex2D(float4), SparseTexture, UPDATE_FREQ_NONE, t0, binding = 8);
RES(SamplerState, uSampler0, UPDATE_FREQ_NONE, s0, binding = 7);

CBUFFER(SparseTextureInfo, UPDATE_FREQ_NONE, b0, binding = 9)
{
	DATA(uint, Width, None);
	DATA(uint, Height, None);
	DATA(uint, pageWidth, None);
	DATA(uint, pageHeight, None);

	DATA(uint, DebugMode, None);
	DATA(uint, ID, None);
	DATA(uint, pad1, None);
	DATA(uint, pad2, None);
	
	DATA(float4, CameraPos, None);
};

STRUCT(PsIn)
{    
  DATA(float4, Position, SV_Position);
  DATA(float2, UV, TEXCOORD0);
	DATA(float3, Normal, NORMAL);
  DATA(float3, Pos, POSITION);
};

float4 PS_MAIN( PsIn In )
{
	INIT_MAIN;
	float4 Out;
  float4 color = float4(0.0, 0.0, 0.0, 0.0);
  float4 outColor = float4(0.0, 0.0, 0.0, 0.0);

  uint minDim     = min(Width, Height);
  uint minPageDim = min(pageWidth, pageHeight);

  float maxLod = floor(log2(float(minDim)) - log2(float(minPageDim)));
  // float LOD = textureQueryLod(sampler2D(SparseTexture, uSampler0), In.UV).y;
  // float LOD = SparseTexture.CalculateLevelOfDetailUnclamped(uSampler0, In.UV);
  float LOD = CalculateLvlTex2D(SparseTexture, uSampler0, In.UV);
  LOD = clamp(LOD, 0.0f, maxLod);

  float minLod = floor(LOD);
	minLod = clamp(minLod, 0.0f, maxLod);

  float localMinLod = minLod;
  
  residency_status residencyCode = 0; 

  LOOP
  do
  {
    // color = SampleLvlTex2D(SparseTexture, uSampler0, In.UV, 0, min(localMinLod, maxLod), residencyCode);
    color = SampleClampedSparseTex2D(SparseTexture, uSampler0, In.UV, min(localMinLod, maxLod), residencyCode);
    localMinLod += 1.0f;

    if(localMinLod > maxLod)
      break;

  }while(!IsFullyMapped(residencyCode));

  uint pageOffset = 0;

  uint pageCountWidth = Width / pageWidth;
  uint pageCountHeight = Height / pageHeight;

  for(int i = 1; i <= int(minLod); i++)
  {
    pageOffset += pageCountWidth * pageCountHeight;

    pageCountWidth = pageCountWidth / 2;
    pageCountHeight = pageCountHeight /2;
  }

  float debugLineWidth = 0.025 / (minLod + 1.0f);

  pageOffset += uint(float(pageCountWidth) * In.UV.x) + uint(float(pageCountHeight) * In.UV.y) * pageCountWidth;

  float NoL = max(dot(In.Normal, -normalize(In.Pos.xyz)), 0.05f);  
  
  float4 debugColor;

  if(DebugMode == 1)
  {
    float4 resultColor = float4(0.0, 0.0, 0.0, 0.0);

    float2 modVal = frac(In.UV * float2(float(pageCountWidth), float(pageCountHeight))) + float2(debugLineWidth, debugLineWidth);

    if(modVal.x < debugLineWidth * 2.0f || modVal.y < debugLineWidth * 2.0f )
    {
      outColor.rgb = lerp(float3(1.0, 0.0, 0.0), float3(0.0, 1.0, 0.0), LOD / maxLod);
      outColor.a = 1.0;
    }

    outColor = lerp(float4(color.rgb * NoL, color.a), float4(outColor.rgb, color.a), outColor.a);
  }  
  else
  {
    outColor = float4(color.rgb * NoL, color.a);
  }

  if (pageOffset < VT_TOTAL_PAGE_COUNT())
  {
    VT_VISIBLE_PAGES(pageOffset) = 1;
  }
  Out = outColor;
	RETURN(Out);
}