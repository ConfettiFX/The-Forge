//
// Copyright (c) 2016 Advanced Micro Devices, Inc. All rights reserved.
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in
// all copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.  IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
// THE SOFTWARE.

/*
 * Copyright (c) 2017-2024 The Forge Interactive Inc.
 * 
 * This file is part of The-Forge
 * (see https://github.com/ConfettiFX/The-Forge).
 * 
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 * 
 *   http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
*/

// Confetti changes
// Culling triangles that intersect the near plane
// Small primitive culling now supports MSAA
// Multi-viewport culling

#include "Shader_Defs.h.fsl"

RES(ByteBuffer, indexDataBuffer, UPDATE_FREQ_NONE, t13, binding = 4);
RES(Buffer(MeshConstants), meshConstantsBuffer, UPDATE_FREQ_NONE, t14, binding = 5);
#include "../../../../../../Common_3/Renderer/VisibilityBuffer/Shaders/FSL/triangle_filtering.h.fsl"

NUM_THREADS(VB_COMPUTE_THEADS, 1, 1)
void CS_MAIN( SV_GroupThreadID(uint3) inGroupId, SV_GroupID(uint3) groupId )
{
	INIT_MAIN;
	if (inGroupId.x == 0)
	{
		for (uint i = 0; i < Get(numViewports); ++i)
			AtomicStore(workGroupIndexCount[i], 0u);

		filterDispatchGroupData = Get(filterDispatchGroupDataBuffer)[groupId.x];
	}

	GroupMemoryBarrier();

	bool cull[NUM_CULLING_VIEWPORTS];
	uint threadOutputSlot[NUM_CULLING_VIEWPORTS];

	for (uint i = 0; i < Get(numViewports); ++i)
	{
		threadOutputSlot[i] = 0;
		cull[i] = true;
	}

	uint batchMeshIndex = filterDispatchGroupData.meshIndex;
	uint batchGeomSet = ((filterDispatchGroupData.geometrySet_faceCount & BATCH_GEOMETRY_MASK) >> BATCH_GEOMETRY_LOW_BIT);
	uint batchFaceCount =  ((filterDispatchGroupData.geometrySet_faceCount & BATCH_FACE_COUNT_MASK) >> BATCH_FACE_COUNT_LOW_BIT);
	uint batchInputIndexOffset = (Get(meshConstantsBuffer)[batchMeshIndex].indexOffset + filterDispatchGroupData.indexOffset);
	uint vertexOffset = Get(meshConstantsBuffer)[batchMeshIndex].vertexOffset;
	bool twoSided = (Get(meshConstantsBuffer)[batchMeshIndex].twoSided == 1);

	uint indices[3] = { 0, 0, 0 };
	if (inGroupId.x < batchFaceCount)
	{
		indices[0] = vertexOffset + LoadByte(Get(indexDataBuffer), (inGroupId.x * 3 + 0 + batchInputIndexOffset) << 2);
		indices[1] = vertexOffset + LoadByte(Get(indexDataBuffer), (inGroupId.x * 3 + 1 + batchInputIndexOffset) << 2);
		indices[2] = vertexOffset + LoadByte(Get(indexDataBuffer), (inGroupId.x * 3 + 2 + batchInputIndexOffset) << 2);

		float4 vert[3] =
		{
			LoadVertex(indices[0]),
			LoadVertex(indices[1]),
			LoadVertex(indices[2])
		};

		for (uint i = 0; i < Get(numViewports); ++i)
		{
			float4x4 worldViewProjection = Get(transform)[i].mvp;
			float4 vertices[3] =
			{
				mul(worldViewProjection, vert[0]),
				mul(worldViewProjection, vert[1]),
				mul(worldViewProjection, vert[2])
			};

			CullingViewPort viewport = Get(cullingViewports)[i];
			cull[i] = FilterTriangle(indices, vertices, !twoSided, viewport.windowSize, viewport.sampleCount);
			if (!cull[i])
				AtomicAdd(workGroupIndexCount[i], 3, threadOutputSlot[i]);
		}
	}

	GroupMemoryBarrier();

	uint accumBatchDrawIndex = filterDispatchGroupData.accumDrawIndex;

	if (inGroupId.x == 0)
	{
		for (uint i = 0; i < Get(numViewports); ++i)
		{
			AtomicAdd(Get(uncompactedDrawArgsRW)[GET_UNCOMPACT_DRAW_INDEX(i,accumBatchDrawIndex)].numIndices, workGroupIndexCount[i], workGroupOutputSlot[i]);
		}
	}

	AllMemoryBarrier();

	for (uint j = 0; j < Get(numViewports); ++j)
	{
		if (!cull[j])
		{
            uint index = AtomicLoad(workGroupOutputSlot[j]);
			
			// We store indexes without vertexOffset because the offset is then stored in indirectDrawArgs and applied by the draw call
			StoreByte(Get(filteredIndicesBuffer)[j], (index + filterDispatchGroupData.outputIndexOffset + threadOutputSlot[j] + 0) << 2, indices[0] - vertexOffset);
			StoreByte(Get(filteredIndicesBuffer)[j], (index + filterDispatchGroupData.outputIndexOffset + threadOutputSlot[j] + 1) << 2, indices[1] - vertexOffset);
			StoreByte(Get(filteredIndicesBuffer)[j], (index + filterDispatchGroupData.outputIndexOffset + threadOutputSlot[j] + 2) << 2, indices[2] - vertexOffset);
		}
	}

	if (inGroupId.x == 0 && groupId.x == filterDispatchGroupData.meshDispatchGroupStart)
	{
		uint outIndexOffset = filterDispatchGroupData.outputIndexOffset;
		uint matID = Get(meshConstantsBuffer)[batchMeshIndex].materialID;

		for (uint i = 0; i < Get(numViewports); ++i)
		{
			uint uncompactIndex = GET_UNCOMPACT_DRAW_INDEX(i,accumBatchDrawIndex);
            Get(uncompactedDrawArgsRW)[uncompactIndex].startIndex = outIndexOffset;
			Get(uncompactedDrawArgsRW)[uncompactIndex].vertexOffset = vertexOffset;
            Get(uncompactedDrawArgsRW)[uncompactIndex].geometrySet = batchGeomSet;
            Get(uncompactedDrawArgsRW)[uncompactIndex].instanceDataIndex = matID;
		}
	}
	RETURN();
}
