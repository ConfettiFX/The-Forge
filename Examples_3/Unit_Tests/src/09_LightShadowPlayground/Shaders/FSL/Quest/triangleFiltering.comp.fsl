//
// Copyright (c) 2016 Advanced Micro Devices, Inc. All rights reserved.
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in
// all copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.  IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
// THE SOFTWARE.

/*
 * Copyright (c) 2017-2022 The Forge Interactive Inc.
 * 
 * This file is part of The-Forge
 * (see https://github.com/ConfettiFX/The-Forge).
 * 
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 * 
 *   http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
*/

// Confetti changes
// Culling triangles that intersect the near plane
// Small primitive culling now supports MSAA
// Multi-viewport culling

#include "Shader_Defs.h.fsl"
#include "cull_resources.h.fsl"

GroupShared(atomic_uint, workGroupOutputSlot[NUM_CULLING_VIEWPORTS]);
GroupShared(atomic_uint, workGroupIndexCount[NUM_CULLING_VIEWPORTS]);

#define ENABLE_CULL_INDEX				1
#define ENABLE_CULL_BACKFACE			1
#define ENABLE_CULL_FRUSTUM				1
#define ENABLE_CULL_SMALL_PRIMITIVES	1
#define ENABLE_GUARD_BAND				0

float4 LoadVertex(uint index)
{
	return float4(asfloat(LoadByte4(Get(vertexDataBuffer), index * 12)).xyz, 1);
}

// Performs all the culling tests given 3 vertices
bool FilterTriangle(uint indices[3], float4 vertices[3], bool cullBackFace, float2 windowSize, uint samples)
{
#if ENABLE_CULL_INDEX
	if (indices[0] == indices[1]
		|| indices[1] == indices[2]
		|| indices[0] == indices[2])
	{
		return true;
	}
#endif
#if ENABLE_CULL_BACKFACE
	if (cullBackFace)
	{
		// Culling in homogeneus coordinates.
		// Read: "Triangle Scan Conversion using 2D Homogeneus Coordinates"
		//       by Marc Olano, Trey Greer
		float3x3 m = float3x3(vertices[0].xyw, vertices[1].xyw, vertices[2].xyw);
		if (determinant(m) > 0)
			return true;
	}
#endif

#if ENABLE_CULL_FRUSTUM || ENABLE_CULL_SMALL_PRIMITIVES
	int verticesInFrontOfNearPlane = 0;

	for (uint i = 0; i < 3; i++)
	{
		if (vertices[i].w < 0)
		{
			++verticesInFrontOfNearPlane;

			// Flip the w so that any triangle that straddles the plane won't be projected onto
			// two sides of the screen
			vertices[i].w *= (-1.0);
		}
		// Transform vertices[i].xy into the normalized 0..1 screen space
		// this is for the following stages ...
		vertices[i].xy /= vertices[i].w * 2;
		vertices[i].xy += float2(0.5, 0.5);
	}
#endif

#if ENABLE_CULL_FRUSTUM
	if (verticesInFrontOfNearPlane == 3)
		return true;

	float minx = min(min(vertices[0].x, vertices[1].x), vertices[2].x);
	float miny = min(min(vertices[0].y, vertices[1].y), vertices[2].y);
	float maxx = max(max(vertices[0].x, vertices[1].x), vertices[2].x);
	float maxy = max(max(vertices[0].y, vertices[1].y), vertices[2].y);

	if ((maxx < 0) || (maxy < 0) || (minx > 1) || (miny > 1))
		return true;
#endif

	// not precise enough to handle more than 4 msaa samples
#if ENABLE_CULL_SMALL_PRIMITIVES
	if (verticesInFrontOfNearPlane == 0)
	{
		const uint SUBPIXEL_BITS = 8;
		const uint SUBPIXEL_MASK = 0xFF;
		const uint SUBPIXEL_SAMPLES = 1 << SUBPIXEL_BITS;

		/*
		Computing this in float-point is not precise enough.
		We switch to a 23.8 representation here which shold match the
		HW subpixel resolution.
		We use a 8-bit wide guard-band to avoid clipping. If
		a triangle is outside the guard-band, it will be ignored.
		That is, the actual viewport supported here is 31 bit, one bit is
		unused, and the guard band is 1 << 23 bit large (8388608 pixels)
		*/

		int2 minBB = int2(1 << 30, 1 << 30);
		int2 maxBB = -minBB;
#if ENABLE_GUARD_BAND			
		bool insideGuardBand = true;
#endif
		for (uint i = 0; i < 3; i++)
		{
			float2 screenSpacePositionFP = vertices[i].xy * windowSize;
#if ENABLE_GUARD_BAND			
			// Check if we should overflow after conversion
			if (screenSpacePositionFP.x < -(1 << 23) ||
				screenSpacePositionFP.x > (1 << 23) ||
				screenSpacePositionFP.y < -(1 << 23) ||
				screenSpacePositionFP.y > (1 << 23))
			{
				insideGuardBand = false;
			}
#endif
			// Scale based on distance from center to msaa sample point
			int2 screenSpacePosition = int2(screenSpacePositionFP * (SUBPIXEL_SAMPLES * samples));
			minBB = min(screenSpacePosition, minBB);
			maxBB = max(screenSpacePosition, maxBB);
		}
#if ENABLE_GUARD_BAND			
		if (insideGuardBand)
#endif
		{
			const uint SUBPIXEL_SAMPLE_CENTER = SUBPIXEL_SAMPLES / 2;
			const uint SUBPIXEL_SAMPLE_SIZE = SUBPIXEL_SAMPLES - 1;
			/* Test is:
			Is the minimum of the bounding box right or above the sample
			point and is the width less than the pixel width in samples in
			one direction.

			This will also cull very long triagles which fall between
			multiple samples.
			*/

			if (any( And(GreaterThan( minBB & SUBPIXEL_MASK, SUBPIXEL_SAMPLE_CENTER ),
				LessThan( maxBB - ((minBB & ~SUBPIXEL_MASK) + SUBPIXEL_SAMPLE_CENTER), SUBPIXEL_SAMPLE_SIZE ))))
			{
				return true;
			}
		}
	}
#endif

	return false;
}

NUM_THREADS(CLUSTER_SIZE, 1, 1)
void CS_MAIN( SV_GroupThreadID(uint3) inGroupId, SV_GroupID(uint3) groupId )
{
	INIT_MAIN;
	if (inGroupId.x == 0)
	{
		UNROLL_N(NUM_CULLING_VIEWPORTS)
		for (uint i = 0; i < NUM_CULLING_VIEWPORTS; ++i)
			AtomicStore(workGroupIndexCount[i], 0u);
	}

	GroupMemoryBarrier();
	
	bool cull[NUM_CULLING_VIEWPORTS];
	uint threadOutputSlot[NUM_CULLING_VIEWPORTS];
	
	UNROLL_N(NUM_CULLING_VIEWPORTS)
	for (uint i = 0; i < NUM_CULLING_VIEWPORTS; ++i)
	{
		threadOutputSlot[i] = 0;
		cull[i] = true;
	}
	
	uint batchMeshIndex = Get(smallBatchDataBuffer)[groupId.x].mMeshIndex;
	uint batchInputIndexOffset = (Get(meshConstantsBuffer)[batchMeshIndex].indexOffset + Get(smallBatchDataBuffer)[groupId.x].mIndexOffset);
	bool twoSided = (Get(meshConstantsBuffer)[batchMeshIndex].twoSided == 1);
	
	uint indices[3] = { 0, 0, 0 };
	if (inGroupId.x < Get(smallBatchDataBuffer)[groupId.x].mFaceCount)
	{
		indices[0] = LoadByte(Get(indexDataBuffer), (inGroupId.x * 3 + 0 + batchInputIndexOffset) << 2);
		indices[1] = LoadByte(Get(indexDataBuffer), (inGroupId.x * 3 + 1 + batchInputIndexOffset) << 2);
		indices[2] = LoadByte(Get(indexDataBuffer), (inGroupId.x * 3 + 2 + batchInputIndexOffset) << 2);
	
		float4 vert[3] =
		{
			LoadVertex(indices[0]),
			LoadVertex(indices[1]),
			LoadVertex(indices[2])
		};
	
    	UNROLL_N(NUM_CULLING_VIEWPORTS)
		for (uint i = 0; i < NUM_CULLING_VIEWPORTS; ++i)
		{
			float4x4 worldViewProjection = Get(mWorldViewProjMat)[i];
			float4 vertices[3] =
			{
				mul(worldViewProjection, vert[0]),
				mul(worldViewProjection, vert[1]),
				mul(worldViewProjection, vert[2])
			};
	
			CullingViewPort viewport = Get(mCullingViewports)[i];
			cull[i] = FilterTriangle(indices, vertices, !twoSided, viewport.mWindowSize, viewport.mSampleCount);
			if (!cull[i])
				AtomicAdd(workGroupIndexCount[i], 3, threadOutputSlot[i]);
		}
	}
	
	GroupMemoryBarrier();
	
	uint accumBatchDrawIndex = Get(smallBatchDataBuffer)[groupId.x].mAccumDrawIndex;
	
	if (inGroupId.x == 0)
	{
		AtomicAdd(Get(uncompactedDrawArgsRW)[0][accumBatchDrawIndex].mNumIndices, workGroupIndexCount[0], workGroupOutputSlot[0]);
		AtomicAdd(Get(uncompactedDrawArgsRW)[1][accumBatchDrawIndex].mNumIndices, workGroupIndexCount[1], workGroupOutputSlot[1]);
		//UNROLL_N(NUM_CULLING_VIEWPORTS)
		//for (uint i = 0; i < NUM_CULLING_VIEWPORTS; ++i)
		//{
		//	AtomicAdd(Get(uncompactedDrawArgsRW)[i][accumBatchDrawIndex].mNumIndices, workGroupIndexCount[i], workGroupOutputSlot[i]);
		//}
	}
	
	AllMemoryBarrier();
	
	UNROLL_N(NUM_CULLING_VIEWPORTS)
	for (uint j = 0; j < NUM_CULLING_VIEWPORTS; ++j)
	{
		if (!cull[j])
		{
            uint index = AtomicLoad(workGroupOutputSlot[j]);
			StoreByte(Get(filteredIndicesBuffer)[j], (index + Get(smallBatchDataBuffer)[groupId.x].mOutputIndexOffset + threadOutputSlot[j] + 0) << 2, indices[0]);
			StoreByte(Get(filteredIndicesBuffer)[j], (index + Get(smallBatchDataBuffer)[groupId.x].mOutputIndexOffset + threadOutputSlot[j] + 1) << 2, indices[1]);
			StoreByte(Get(filteredIndicesBuffer)[j], (index + Get(smallBatchDataBuffer)[groupId.x].mOutputIndexOffset + threadOutputSlot[j] + 2) << 2, indices[2]);
		}
	}
	
	if (inGroupId.x == 0 && groupId.x == Get(smallBatchDataBuffer)[groupId.x].mDrawBatchStart)
	{
		uint outIndexOffset = Get(smallBatchDataBuffer)[groupId.x].mOutputIndexOffset;
	
		UNROLL_N(NUM_CULLING_VIEWPORTS)
		for (uint i = 0; i < NUM_CULLING_VIEWPORTS; ++i)
		{
			Get(uncompactedDrawArgsRW)[i][accumBatchDrawIndex].mStartIndex = outIndexOffset;
			Get(uncompactedDrawArgsRW)[i][accumBatchDrawIndex].mMaterialID = Get(meshConstantsBuffer)[batchMeshIndex].materialID;
		}
	}
	RETURN();
}
