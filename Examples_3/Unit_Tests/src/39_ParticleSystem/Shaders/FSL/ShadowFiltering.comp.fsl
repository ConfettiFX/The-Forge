/*
 * Copyright (c) 2017-2025 The Forge Interactive Inc.
 * 
 * This file is part of The-Forge
 * (see https://github.com/ConfettiFX/The-Forge).
 * 
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 * 
 *   http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
*/

#include "ShaderDefs.h.fsl"
#include "VisibilityBufferResources.h.fsl"
#include "ShadowFiltering.srt.h"

ROOT_SIGNATURE(ComputeRootSignature)
NUM_THREADS(SHADOWMAP_BLUR_THREAD_COUNT, SHADOWMAP_BLUR_THREAD_COUNT, 1)
void CS_MAIN( SV_DispatchThreadID(uint3) threadInDispatch, SV_GroupThreadID(uint3) threadInGroup, SV_GroupID(uint3) groupID )
{
	INIT_MAIN;

	if (threadInDispatch.x >= gShadowFilteringConstants.ShadowmapSize.x || threadInDispatch.y >= gShadowFilteringConstants.ShadowmapSize.y)
	{
		RETURN();
	}

	const int stepCount = 1;
	float sigma = float(stepCount * 2 + 1) / 2.0;
	
	uint horizontal = gShadowFilteringConstants.Horizontal;
	uint readIdx = horizontal == 1 ? 0 : 1;
	uint writeIdx = 1 - readIdx;

	float currSample;

	BeginNonUniformResourceIndex(readIdx, 2);
	currSample = LoadRWTex2D(gShadowTexturesRW[readIdx], threadInDispatch.xy).x;
	EndNonUniformResourceIndex();

	const float anisoSize = 1;
	const float anisoStep = anisoSize / stepCount;

	float val = 0.0;
	float currOffset = -anisoStep * stepCount;
	float totWeight = 0.0;

	// TODO:
	// - Distance map should probably be a separate R16G16 texture
	// - Don't filter things that are near in screen space but far in world space (use distance map)
	// - Optimize texture access using group shared memory

	for (int i=-stepCount; i<=stepCount; i++)
	{
		int intOffset = int(round(currOffset));
		int2 coords = int2(threadInDispatch.xy) + int2(intOffset * uint(horizontal == 1), intOffset * uint(horizontal == 0));
		coords = clamp(coords, int2(0,0), int2(gShadowFilteringConstants.ShadowmapSize - 1));

		float shadow;
		BeginNonUniformResourceIndex(readIdx, 2);
		shadow = LoadRWTex2D(gShadowTexturesRW[readIdx], coords).x;
		EndNonUniformResourceIndex();

		float a = float(abs(i)) / sigma;
		a *= a;
		float weight = exp((-0.5) * a);

		val += shadow * weight;
		totWeight += weight;

		currOffset += anisoStep;
	}

	val /= totWeight;

	Write2D(gShadowTexturesRW[writeIdx], threadInDispatch.xy, val);
	
	RETURN();
}
