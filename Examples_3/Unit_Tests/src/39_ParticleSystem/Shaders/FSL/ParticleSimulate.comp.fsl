/*
 * Copyright (c) 2017-2025 The Forge Interactive Inc.
 *
 * This file is part of The-Forge
 * (see https://github.com/ConfettiFX/The-Forge).
 *
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

#include "../../../../../../Common_3/Renderer/ParticleSystem/Shaders/ParticleShared.h.fsl"
#include "../../../../../../Common_3/Renderer/ParticleSystem/Shaders/ParticleSets.h.fsl"
#include "ShaderDefs.h.fsl"
#include "VisibilityBufferResources.h.fsl"
#include "Particle.srt.h"
#include "SceneLighting.h.fsl"
#include "../../../Graphics/ShaderUtilities.h.fsl"
#include "../../../../../../Common_3/Renderer/ParticleSystem/Shaders/ParticleBoids.h.fsl"
#include "../../../../../../Common_3/Renderer/ParticleSystem/Shaders/ParticleRandom.h.fsl"
#include "../../../../../../Common_3/Renderer/ParticleSystem/Shaders/ParticleUtils.h.fsl"


STATIC const float2 quadUVs[QUAD_VERTEX_COUNT] = 
{
	float2(0.0f, 0.0f),
	float2(1.0f, 0.0f),
	float2(1.0f, 1.0f),
	float2(0.0f, 1.0f)
};

INLINE bool IsPointInTriangle(float2 p, float2 a, float2 b, float2 c, inout(float2) baryCoords)
{
	float u = ((b.y - c.y)*(p.x - c.x) + (c.x - b.x)*(p.y - c.y)) / ((b.y - c.y)*(a.x - c.x) + (c.x - b.x)*(a.y - c.y));
	float v = ((c.y - a.y)*(p.x - c.x) + (a.x - c.x)*(p.y - c.y)) / ((b.y - c.y)*(a.x - c.x) + (c.x - b.x)*(a.y - c.y));
	float w = 1 - u - v;

	if (0 <= u  && u <= 1 && 0 <= v && v <= 1 && 0 <= w && w <= 1)
	{
		baryCoords.x = u;
		baryCoords.y = v;
		return true;
	}

    return false;
}


// Current batch size is 32x1, each thread simulates and renders 1 particle
ROOT_SIGNATURE(ComputeRootSignature)
NUM_THREADS(PARTICLES_BATCH_X, PARTICLES_BATCH_Y, 1)
void CS_MAIN(SV_GroupThreadID(uint3) inGroupThreadId, SV_DispatchThreadID(uint3) inDispatchThreadId)
{
	INIT_MAIN;

	uint threadCount = uint(ceil(sqrt(float(MAX_PARTICLES_COUNT) / (PARTICLES_BATCH_X * PARTICLES_BATCH_Y))));
	uint particleIdx = inDispatchThreadId.y * threadCount * PARTICLES_BATCH_X + inDispatchThreadId.x;
	uint groupIdx = inGroupThreadId.x + inGroupThreadId.y * PARTICLES_BATCH_X;

	if (particleIdx == 0)
	{
		gParticleRenderIndirectDataRW[0] = 6;
		gParticleRenderIndirectDataRW[1] = 0;
		gParticleRenderIndirectDataRW[2] = 0;
		gParticleRenderIndirectDataRW[3] = 0;
#ifdef _DEBUG
		gParticleStatsBufferRW[0].VisibleParticlesCount = 0;
		gParticleStatsBufferRW[0].VisibleLightsCount = 0;
		gParticleStatsBufferRW[0].SoftwareRasterized = 0;
		gParticleStatsBufferRW[0].HardwareRasterized = 0;

		UNROLL_N(PARTICLE_BUFFER_SECTION_COUNT)
		for (uint i=0; i<PARTICLE_BUFFER_SECTION_COUNT; i++)
		{
			gParticleStatsBufferRW[0].AliveCount[i] = 0;
			gParticleStatsBufferRW[0].AllocatedCount[i] = 0;
		}
#endif
	}

	GroupMemoryBarrier();

	if (particleIdx >= MAX_PARTICLES_COUNT)
	{
		RETURN();
	}

	uint rngSeed = gParticleConstantBuffer.Seed * particleIdx;

	uint particleCount;
	uint particleBufferOffset;

#ifdef _DEBUG
	// Find the section where we are for debug purposes
	uint currBufferSection = GetCurrentBufferSection(particleIdx);
#endif

	uint Bitfield = gBitfieldBufferRW[particleIdx];
	uint setIndex = Bitfield & PARTICLE_BITFIELD_SET_INDEX_MASK;
	if (Bitfield == 0)
	{
		RETURN();
	}

	if (particleIdx - gParticleSetsBuffer[setIndex].StartIdx > ceil(gParticleSetsBuffer[setIndex].ParticlesPerSecond * gParticleSetsBuffer[setIndex].PositionAndInitialAge.w))
	{
		gBitfieldBufferRW[particleIdx] = 0;
		RETURN();
	}

	// Initialize particle if it has just been uploaded
	float2 alivePeriod = unpack2Floats(gParticleSetsBuffer[setIndex].AlivePeriod);
	if ((Bitfield & PARTICLE_BITFIELD_REQUIRES_INIT) > 0 && TimeOfDayInParticleAlivePeriod(gParticleConstantBuffer.TimeOfDay, alivePeriod))
	{
		Bitfield = (Bitfield & PARTICLE_BITFIELD_SET_INDEX_MASK) | PARTICLE_BITFIELD_LIGHT_CULLED | PARTICLE_BITFIELD_IS_ALLOCATED | gParticleSetsBuffer[setIndex].ParticleSetBitfield;
	
		float Age = (float(particleIdx - gParticleSetsBuffer[setIndex].StartIdx) / -gParticleSetsBuffer[setIndex].ParticlesPerSecond);
		SaveParticle(gParticleSetsBuffer[setIndex], particleIdx, Bitfield, float3(0, 0, 0), Age, float3(0, 0, 0), 0);

#ifdef _DEBUG
		uint foo;
		AtomicAdd(gParticleStatsBufferRW[0].AllocatedCount[setIndex], 1, foo);
#endif
		RETURN();
	}

	if ((Bitfield & PARTICLE_BITFIELD_IS_ALLOCATED) == 0 || !IsParticleSetVisible(gParticleSetsBuffer[setIndex]))
	{
		RETURN();
	}

	ParticleData particleData;
	GetParticleData(particleData, gParticlesDataBufferRW, particleIdx);
	float Age;

	// Particle set attributes
	const ParticleSet particleSet = gParticleSetsBuffer[setIndex];
	particleCount = uint(UnpackMaxParticles(particleSet));

	// Particle attributes
	float3 Position; 
	float4 velocityAge;

	UnpackParticle(particleSet, particleData, Position, velocityAge);
	Age = velocityAge.w;

	float2 steeringSpeed = unpack2Floats(particleSet.SteeringStrengthMinSpeed);
	float maxSpeed = unpack2Floats(particleSet.MaxSizeAndSpeed).y;
	float minSpeed = steeringSpeed.y;
	steeringSpeed.y = UnpackSpeed(particleSet, particleIdx);

	// Worst case simulate at 15 FPS
	float deltaTime = min(0.066, gParticleConstantBuffer.TimeDelta);
	Age += (Bitfield & PARTICLE_BITFIELD_IS_ALIVE) == 0 ? deltaTime : -deltaTime;

	if ((Bitfield & PARTICLE_BITFIELD_IS_ALIVE) == 0)
	{
		if (Age >= 0.0f)
		{
			// Mechanism used to let particles fade out instead of disappearing abruptly
			if (!TimeOfDayInParticleAlivePeriod(gParticleConstantBuffer.TimeOfDay, alivePeriod))
			{
				Bitfield |= PARTICLE_BITFIELD_REQUIRES_INIT;
				SaveParticle(particleSet, particleIdx, Bitfield, Position, Age, velocityAge.xyz, 0);
				RETURN();
			}
#ifdef _DEBUG
			uint foo;
			AtomicAdd(gParticleStatsBufferRW[0].AliveCount[currBufferSection], 1, foo);
#endif
			// Initialize particle.
			Age = particleSet.PositionAndInitialAge.w - Age;
			Position = (UnpackAttractorIndex(particleSet) != 0 ? gParticleConstantBuffer.SeekPositions[0].xyz : particleSet.PositionAndInitialAge.xyz) + randFloat3_negOneToPlusOne(rngSeed) * UnpackParticleSetSize(particleSet.SpawnVolume);

			if((Bitfield & PARTICLE_BITFIELD_SPAWN_FROM_SHADOW_MAP) > 0)
			{
				float4 posLS = mul(gPerFrameVBConstants.transform[VIEW_SHADOW].mvp.mat, float4(Position, 1.0f));
				posLS /= posLS.w;
				posLS.y *= -1;
				posLS.xy = posLS.xy * 0.5 + f2(0.5);

				if (all(GreaterThan(posLS.xy, 0.0f)) && all(LessThan(posLS.xy, 1.0f)))
				{
					int2 pixelPos = int2(posLS.xy * 2048.f);
					float smDepth = LoadTex2D(gShadowMap, NO_SAMPLER, pixelPos, 0).r;
					float minDepth = 1e9;
					int2 minPixel = pixelPos;
					int2 pixelOffsets[4] = {
						int2(0, -1),
						int2(0, 1),
						int2(-1, 0),
						int2(0, 1)
					};
					float d[4];
					UNROLL
					for (int i = 0; i < 4; ++i)
					{
						int2 pix = pixelPos + pixelOffsets[i];
						d[i] = LoadTex2D(gShadowMap, NO_SAMPLER, pix, 0).r;
						if (d[i] < minDepth)
						{
							minDepth = d[i];
							minPixel = pix + pixelOffsets[i] * 3;
						}
					}
					float dx = abs(d[0] - d[1]);
					float dy = abs(d[2] - d[3]);

					float edgeValue = sqrt(dx * dx + dy * dy);
					if(edgeValue < 0.07){
						Age = -1.f;
					}

					posLS.xy = float2(minPixel) / 2048.f;
					//float2 ndcXY = posLS.xy * 2.0f - 1.0f;
					float4 clipPos = float4(posLS.x * 2.f - 1.f, (1.0 - posLS.y) * 2.0f - 1.0f, smDepth, 1.0f);
					clipPos = mul(gPerFrameVBConstants.transform[VIEW_SHADOW].invVP.mat, clipPos);
					clipPos /= clipPos.w;
					Position = clipPos.xyz;
				}
				else{
					Age = -1.f;
				}
			}

			velocityAge.xyz = UnpackStartVelocity(particleSet).xyz + clamp(UnpackStartVelocity(particleSet).w, 0.0, 1000.0) * randFloat3_negOneToPlusOne(rngSeed);

			Bitfield = (Bitfield & PARTICLE_BITFIELD_SET_INDEX_MASK) | PARTICLE_BITFIELD_LIGHT_CULLED | PARTICLE_BITFIELD_IS_ALLOCATED | gParticleSetsBuffer[setIndex].ParticleSetBitfield;
			Bitfield |= length(UnpackAcceleration(particleSet).xyz) > 0.01 ? PARTICLE_BITFIELD_IS_ACCELERATING : 0;
			Bitfield |= PARTICLE_BITFIELD_IS_ALIVE;
		}

		SaveParticle(particleSet, particleIdx, Bitfield, Position, Age, velocityAge.xyz, 0);
		RETURN();
	}
	else if (Age < 0)
	{
		// Kill particle
		Bitfield &= ~PARTICLE_BITFIELD_IS_ALIVE;
		// Light isn't visible anymore
		Bitfield |= PARTICLE_BITFIELD_LIGHT_CULLED;

		Age = 0;
#ifdef _DEBUG
		uint foo;
		AtomicAdd(gParticleStatsBufferRW[0].AliveCount[currBufferSection], -1, foo);
#endif

		SaveParticle(particleSet, particleIdx, Bitfield, Position, Age, velocityAge.xyz, 0);
		RETURN();
	}

	// Simulate particle
	// Add noise
	float2 LightRadiusNoiseIntensity = unpack2Floats(particleSet.LightRadiusAndVelocityNoise);
	LightRadiusNoiseIntensity.x *= UnpackFadeValue(Age / particleSet.PositionAndInitialAge.w, particleSet.StartColor, particleSet.EndColor);
	velocityAge.xyz += (LightRadiusNoiseIntensity.y > 0 && (Bitfield & PARTICLE_BITFIELD_HAS_COLLIDED) == 0) ?
		randFloat3_negOneToPlusOne(rngSeed) * 0.25 * LightRadiusNoiseIntensity.y :
		float3(0,0,0);

	Position += velocityAge.xyz * deltaTime;
	
	float4 vertexPos = float4(Position, 1.0f);
	float4 vertexClipSpace = mul(gParticleConstantBuffer.ViewProjTransform, float4(Position, 1.0f));
	float3 vertexNDC = vertexClipSpace.xyz / vertexClipSpace.w;
	float2 uv;

	if ((Bitfield & PARTICLE_BITFIELD_COLLIDE_WITH_DEPTH_BUFFER) > 0)
	{
		uv = vertexNDC.xy * 0.5 + 0.5;
		uv.y = 1.0 - uv.y;

		float particleDepth = linearizeDepthReverseZ(vertexNDC.z, gParticleConstantBuffer.CameraPlanes.x, gParticleConstantBuffer.CameraPlanes.y);
		float depth = linearizeDepthReverseZ(SampleLvlTex2D(gDepthBuffer, gSamplerPointClamp, uv, 0).r, gParticleConstantBuffer.CameraPlanes.x, gParticleConstantBuffer.CameraPlanes.y);
		
		// Particle collided
		if (particleDepth > depth && abs(particleDepth - depth) < PARTICLE_COLLISION_THRESHOLD && uv.x <= 1.0f && uv.x >= 0.0f && uv.y <= 1.0f && uv.y >= 0.0f)
		{
			Bitfield = (Bitfield & ~(PARTICLE_BITFIELD_IS_ACCELERATING
				| PARTICLE_BITFIELD_COLLIDE_WITH_DEPTH_BUFFER
				| PARTICLE_BITFIELD_BILLBOARD_MODE_BITS_MASK
			) | PARTICLE_BITFIELD_HAS_COLLIDED);

			Position -= deltaTime * velocityAge.xyz * 3.f;
			velocityAge.xyz = float3(0,0,0);
		}
	}

	float animationTime = particleData.AnimationTime;
	if ((Bitfield & PARTICLE_BITFIELD_HAS_COLLIDED) > 0)
		animationTime += deltaTime * UnpackAnimationSpeed(particleSet);

	switch (particleSet.ParticleSetBitfield & PARTICLE_BITFIELD_TYPE_BITS_MASK)
	{
		case PARTICLE_BITFIELD_TYPE_NONE:
		case PARTICLE_BITFIELD_TYPE_RAIN:
		{
			// Accelerate
			velocityAge.xyz += deltaTime * (((Bitfield & PARTICLE_BITFIELD_IS_ACCELERATING) > 0) ? UnpackAcceleration(particleSet).xyz : float3(0,0,0));
		}
		break;

		case PARTICLE_BITFIELD_TYPE_BOIDS:
		{
			float3 attractorPos;
			float minDist = 1000000;
			for (uint i=0; i<MAX_PARTICLE_ATTRACTORS_COUNT; i++)
			{
				float len = length(Position - gParticleConstantBuffer.SeekPositions[i].xyz);
				if (len < minDist)
				{
					minDist = len;
					attractorPos = gParticleConstantBuffer.SeekPositions[i].xyz;
				}
			}

			float2 currForce = unpack2Floats(particleSet.BoidsSeparationSeekStrength);
			float3 steering = float3(0,0,0);
			uint validAmount = 1;
			float3 avgPosition = float3(0,0,0), avgVelocity = float3(0,0,0);

			const uint sampleCount = 8;
			UNROLL_N(sampleCount)
			for (uint i=0; i<sampleCount; i++)
			{
				float3 otherPos;
				float4 otherVel;
				ParticleData particleData;
				GetParticleData(particleData, gParticlesDataBufferRW, particleIdx + int(i)-sampleCount/2);
				UnpackParticle(particleSet, particleData, otherPos, otherVel);

#if defined(DETERMINISTIC_SIMULATION)
				if (int(i)-sampleCount/2 != 0)
					continue;
#else
				if (i == sampleCount / 2 || length(otherPos) < 0.001)
					continue;
#endif
				steering += Separation(Position, otherPos, currForce.x);
				avgPosition += otherPos;
				avgVelocity += otherVel.xyz;
				validAmount++;
			}
			
			// Add a small bias to avoid NaNs when normalizing
			float nanBias = 0.001;

			validAmount = max(validAmount, uint(1));
			
			avgPosition /= validAmount;
			avgVelocity /= validAmount;

			steering += Seek(Position, velocityAge.xyz, UnpackAttractorIndex(particleSet) != 0 ? attractorPos : particleSet.PositionAndInitialAge.xyz, steeringSpeed.y) * currForce.y;
			
			currForce = unpack2Floats(particleSet.BoidsCohesionAlignmentStrength);
#if defined(DETERMINISTIC_SIMULATION)
			currForce *= 0.5;
#endif
			steering += Cohesion(Position, avgPosition, currForce.x);
			if (length(velocityAge.xyz - avgVelocity) > 0.01)
				steering += Alignment(velocityAge.xyz, avgVelocity, currForce.y);

			if (dot(steering, steering) > steeringSpeed.x * steeringSpeed.x)
				steering = normalize(steering) * steeringSpeed.x;

			velocityAge.xyz += steering;
		}
		break;
	}

	if (length(velocityAge.xyz) != 0 && dot(velocityAge.xyz, velocityAge.xyz) > steeringSpeed.y * steeringSpeed.y)
		velocityAge.xyz = normalize(velocityAge.xyz) * steeringSpeed.y;

	// Reset culling bit to 0
	Bitfield &= ~PARTICLE_BITFIELD_LIGHT_CULLED;

	float blinkFactor = UnpackLightModulationFactor(particleSet, gParticleConstantBuffer.Time, particleIdx);
	// Cull if firefly is off
	if ((Bitfield & PARTICLE_BITFIELD_LIGHTING_MODE_LIGHT) > 0 && blinkFactor <= 0.0)
	{
		Bitfield |= PARTICLE_BITFIELD_LIGHT_CULLED;
	}

	// Cull lights
	if ((Bitfield & PARTICLE_BITFIELD_LIGHTING_MODE_LIGHT) > 0 && (Bitfield & PARTICLE_BITFIELD_LIGHTING_MODE_LIGHTNSHADOW) == 0)
	{
		float fov = 2.0 * atan(1.0 / gParticleConstantBuffer.ProjTransform[1][1]);
		float lightRadiusNDC = 2.0 * LightRadiusNoiseIntensity.x * (1 / tan(fov * 0.5f)) / vertexClipSpace.w;
		float ratio = float(gParticleConstantBuffer.ScreenSize.x) / float(gParticleConstantBuffer.ScreenSize.y);

		lightRadiusNDC *= ratio;

		float clusterRadius = length(float2(1, 1));
		float distanceToCenter = length(vertexNDC.xy);

		if (distanceToCenter - clusterRadius >= abs(lightRadiusNDC) || vertexClipSpace.w < -abs(lightRadiusNDC))
		{
			Bitfield |= PARTICLE_BITFIELD_LIGHT_CULLED;
			SaveParticle(particleSet, particleIdx, Bitfield, Position, Age, velocityAge.xyz, animationTime);

			RETURN();
		}
	}

#ifdef _DEBUG
	if ((Bitfield & PARTICLE_BITFIELD_LIGHTING_MODE_LIGHT) > 0 && (Bitfield & PARTICLE_BITFIELD_LIGHTING_MODE_LIGHTNSHADOW) == 0)
	{
		uint foo;
		AtomicAdd(gParticleStatsBufferRW[0].VisibleLightsCount, 1, foo);
	}
#endif

	SaveParticle(particleSet, particleIdx, Bitfield, Position, Age, velocityAge.xyz, animationTime);

	if (animationTime >= 1.0f)
	{
		RETURN();
	}

	/*********************************/
	// RASTERIZATION
	/*********************************/
	float scale = UnpackParticleSize(particleSet, particleIdx, 1.0 - (Age / particleSet.PositionAndInitialAge.w));
	if ((Bitfield & PARTICLE_BITFIELD_LIGHTING_MODE_LIGHT) > 0)
		scale *= max(blinkFactor, 0.2);
	if ((Bitfield & PARTICLE_BITFIELD_HAS_COLLIDED) > 0 && (Bitfield & PARTICLE_BITFIELD_TYPE_RAIN) > 0)
		scale = ANIMATION_TEXTURE_SCALE_FACTOR;

	float4 quadVertices[QUAD_VERTEX_COUNT] = {
		float4(-scale, -scale, 0.0f, 1.0f),
		float4(scale, -scale, 0.0f, 1.0f),
		float4(scale, scale, 0.0f, 1.0f),
		float4(-scale, scale, 0.0f, 1.0f),
	};

	vertexNDC = mul(gParticleConstantBuffer.ViewTransform, vertexPos).xyz;
	velocityAge.xyz = mul(gParticleConstantBuffer.ViewTransform, float4(velocityAge.xyz, 0.0)).xyz;

	UNROLL_N(QUAD_VERTEX_COUNT)
	for (uint i=0; i<QUAD_VERTEX_COUNT; i++)
	{
		if ((Bitfield & PARTICLE_BITFIELD_BILLBOARD_MODE_VELOCITY_ORIENTED) > 0)
		{
			float3 forward = float3(0.0f, 0.0f, 1.0f);
			const float3 up = normalize(velocityAge.xyz);
			const float3 right = cross(up, forward);
			forward = cross(up, right);

			float4x4 billboard = make_f4x4_cols(float4(right, 0), float4(up, 0), float4(forward, 0), float4(vertexPos.xyz, 1.0f));
			quadVertices[i] = mul(billboard, float4(quadVertices[i].xyz, 1.0));
			quadVertices[i] = mul(gParticleConstantBuffer.ViewProjTransform, quadVertices[i]);
		}
		else
		{
			float3 stretchVec = dot(quadVertices[i].xyz, velocityAge.xyz) * velocityAge.xyz;
			if (length(stretchVec) > particleSet.VelocityStretch)
				stretchVec = normalize(stretchVec) * particleSet.VelocityStretch;
			quadVertices[i].xyz += stretchVec;
			quadVertices[i].xyz += vertexNDC;

			// Projection
			quadVertices[i] = mul(gParticleConstantBuffer.ProjTransform, quadVertices[i]);
		}
	}

	// view space
	vertexPos = vertexClipSpace;

	// NDC but XY in [0, 1] range
	vertexPos /= vertexPos.w;
	vertexPos.xy *= 0.5f;
	vertexPos.y *= -1.0f;
	vertexPos.xy += float2(0.5f, 0.5f);

	float2 minMaxX;
	float2 minMaxY;
	
	UNROLL_N(QUAD_VERTEX_COUNT)
	for (uint i=0; i<QUAD_VERTEX_COUNT; i++)
	{
		quadVertices[i] /= quadVertices[i].w;
		quadVertices[i].xy *= 0.5f;
		quadVertices[i].y *= -1.0f;
		quadVertices[i].xy += float2(0.5f, 0.5f);

		if (i == 0)
		{
			minMaxX = float2(quadVertices[0].x, quadVertices[0].x);
			minMaxY = float2(quadVertices[0].y, quadVertices[0].y);
		}

		minMaxX.x = min(minMaxX.x, quadVertices[i].x);
		minMaxX.y = max(minMaxX.y, quadVertices[i].x);
		minMaxY.x = min(minMaxY.x, quadVertices[i].y);
		minMaxY.y = max(minMaxY.y, quadVertices[i].y);
	}

	// Don't rasterize particles that are too small (1/4th of a pixel)
	if (abs(minMaxX.y - minMaxX.x) < 1.0/(gParticleConstantBuffer.ScreenSize.x*2) || abs(minMaxY.y - minMaxY.x) < 1.0/(gParticleConstantBuffer.ScreenSize.x*2))
	{
		RETURN();
	}

	// check if the particle is visible in the camera
	if (vertexPos.z < 0.0f || minMaxX.y < 0.0f || minMaxX.x > 1.0f || minMaxY.y < 0.0f || minMaxY.x > 1.0f)
	{
		RETURN();
	}

	vertexPos.xy *= float2(gParticleConstantBuffer.ScreenSize);
	float2 minMaxU = minMaxX;
	float2 minMaxV = minMaxY;

	UNROLL_N(QUAD_VERTEX_COUNT)
	for (uint i=0; i<QUAD_VERTEX_COUNT; i++)
		quadVertices[i].xy *= float2(gParticleConstantBuffer.ScreenSize);

	for (uint i=0; i<2; i++)
	{
		minMaxX[i] = clamp(minMaxX[i] * float(gParticleConstantBuffer.ScreenSize.x), 0.0f, float(gParticleConstantBuffer.ScreenSize.x) - 1);
		minMaxY[i] = clamp(minMaxY[i] * float(gParticleConstantBuffer.ScreenSize.y), 0.0f, float(gParticleConstantBuffer.ScreenSize.y) - 1);
	}

	// Leave particles that are too big to the hardware rasterizer
	if ((minMaxX.y - minMaxX.x) * (minMaxY.y - minMaxY.x) > PARTICLE_HW_RASTERIZATION_THRESHOLD)
	{
		uint hwRasterizerIdx;
		AtomicAdd(gParticleRenderIndirectDataRW[1], 1, hwRasterizerIdx);
		gParticlesToRasterizeRW[hwRasterizerIdx] = particleIdx;
#if defined(_DEBUG)
		AtomicAdd(gParticleStatsBufferRW[0].HardwareRasterized, 1, hwRasterizerIdx);
#endif
		RETURN();
	}

	float speedOverMaxSpeed = maxSpeed == minSpeed ? 0.0 : (length(velocityAge.xyz) - minSpeed) / (maxSpeed - minSpeed);
	uint texIndex = (Bitfield & PARTICLE_BITFIELD_HAS_COLLIDED) > 0 ? UnpackSecondaryTextureIndex(particleSet) :
		UnpackPrimaryTextureIndex(gParticleSetsBuffer[setIndex]);
	float2 tileOffset = float2(0,0);
	uint2 tileAmount = uint2(1,1);
	
	if ((Bitfield & PARTICLE_BITFIELD_HAS_COLLIDED) > 0)
	{
		uint frameIndex = uint(animationTime * UnpackAnimationFrameCount(particleSet));
		tileAmount = uint2(UnpackAnimationTilesX(particleSet), UnpackAnimationTilesY(particleSet));
		frameIndex = (Bitfield & PARTICLE_BITFIELD_LOOP_ANIMATION) > 0 ? (frameIndex % (tileAmount.x * tileAmount.y)) : clamp(frameIndex, uint(0), tileAmount.x * tileAmount.y);
		
		tileOffset = float2(frameIndex % tileAmount.x, frameIndex / tileAmount.x);
		tileOffset /= float2(tileAmount);
	}

	if (minMaxY.y - minMaxY.x <= 1.0f || minMaxX.y - minMaxX.x <= 1.0f)
	{
		if (vertexPos.z < LoadTex2D(gDepthBuffer, NO_SAMPLER, int2(minMaxX.y, minMaxY.y), 0).x)
		{
			RETURN();
		}

		// Sample texture
		float4 texColor = float4(0,0,0,0);
		BeginNonUniformResourceIndex(texIndex, MAX_PARTICLE_TEXTURES_COUNT);
			texColor = SampleLvlTex2D(ParticleTextures[texIndex], gSamplerBilinearClamp, float2(0.5, 0.5) / float2(tileAmount) + tileOffset, 0);
		EndNonUniformResourceIndex();

		// Modulate color
		texColor = texColor * ((particleSet.ParticleSetBitfield & PARTICLE_BITFIELD_MODULATION_TYPE_LIFETIME) > 0 ? 
			UnpackParticleColorLifetime(particleSet.StartColor, particleSet.EndColor, 1.0 - Age / particleSet.PositionAndInitialAge.w) : 
			UnpackParticleColorSpeed(particleSet.StartColor, particleSet.EndColor, 1.0 - Age / particleSet.PositionAndInitialAge.w, pow(speedOverMaxSpeed, PARTICLE_COLOR_OVER_SPEED_STRENGTH))); 
		
		texColor.xyz = (Bitfield & PARTICLE_BITFIELD_HAS_COLLIDED) > 0 ? lerp(texColor.xyz, GetAmbientLight(float3(0, 1, 0), 0.0f), SPLASHES_AMBIENT_LIGHT_INTENSITY) : texColor.xyz;
		texColor.w *= 0.2f * UnpackAlpha(particleSet, particleIdx);
		texColor = (Bitfield & PARTICLE_BITFIELD_TYPE_RAIN) > 0 ? RefractLight(texColor, uv, float2((minMaxU.x + minMaxU.y) / 2.f, (minMaxV.x + minMaxV.y) / 2.f)) : texColor;

		SaveTransparencyEntry(gParticleConstantBuffer.ScreenSize, uint2(minMaxX.y, minMaxY.y), texColor, vertexPos.z);

#ifdef _DEBUG
		uint foo;
		AtomicAdd(gParticleStatsBufferRW[0].VisibleParticlesCount, 1, foo);
		AtomicAdd(gParticleStatsBufferRW[0].SoftwareRasterized, 1, foo);
#endif

		RETURN();
	}

	for (int y = int(minMaxY.x); y < int(minMaxY.y); y++)
	{
		for (int x = int(minMaxX.x); x < int(minMaxX.y); x++)
		{
            float2 baryCoords2D = float2(0,0);
            uint toAdd = 2;
            
            // Don't draw outside of the 2 triangles that form the quad
            if (IsPointInTriangle(float2(x, y), quadVertices[2].xy, quadVertices[1].xy, quadVertices[0].xy, baryCoords2D))
                toAdd = 0;
            else if (IsPointInTriangle(float2(x, y), quadVertices[3].xy, quadVertices[2].xy, quadVertices[0].xy, baryCoords2D))
                toAdd = 1;
            if (toAdd == 2)
                continue;
                
            float3 baryCoords = float3(baryCoords2D.x, baryCoords2D.y,0);       

            // Compute UVs using the barycentric coordinates we found
            baryCoords.z = 1.0f - baryCoords.x - baryCoords.y;
            uv = float2(0,0);
            for (uint i=0; i<3; i++)
                uv += quadUVs[2 - i + (i!=2 ? toAdd : 0)] * baryCoords[i];
			uv.y = 1.0 - uv.y;

            // Sample texture
            float4 texColor = float4(0,0,0,0);
            BeginNonUniformResourceIndex(texIndex, MAX_PARTICLE_TEXTURES_COUNT);
                texColor = SampleLvlTex2D(ParticleTextures[texIndex], gSamplerBilinearClamp, uv / float2(tileAmount) + tileOffset, 0);
            EndNonUniformResourceIndex();

			// Modulate color
            texColor = texColor * ((particleSet.ParticleSetBitfield & PARTICLE_BITFIELD_MODULATION_TYPE_LIFETIME) > 0 ?
                UnpackParticleColorLifetime(particleSet.StartColor, particleSet.EndColor, 1.0 - Age / particleSet.PositionAndInitialAge.w) :
                UnpackParticleColorSpeed(particleSet.StartColor, particleSet.EndColor, 1.0 - Age / particleSet.PositionAndInitialAge.w, pow(speedOverMaxSpeed, PARTICLE_COLOR_OVER_SPEED_STRENGTH)));
			texColor.xyz = (Bitfield & PARTICLE_BITFIELD_HAS_COLLIDED) > 0 ? lerp(texColor.xyz, GetAmbientLight(float3(0, 1, 0), 0.0f), SPLASHES_AMBIENT_LIGHT_INTENSITY) : texColor.xyz;
            texColor.w *= UnpackAlpha(particleSet, particleIdx);

			texColor = (Bitfield & PARTICLE_BITFIELD_TYPE_RAIN) > 0 && (Bitfield & PARTICLE_BITFIELD_HAS_COLLIDED) == 0 ? RefractLight(texColor, uv, float2((minMaxU.x + minMaxU.y) / 2, (minMaxV.x + minMaxV.y) / 2)) : texColor;
			if (texColor.w <= 0.0)
				continue;

			float depthBufferSample = LoadTex2D(gDepthBuffer, NO_SAMPLER, int2(x, y), 0).x;
            if (vertexPos.z < depthBufferSample)
                continue;

            SaveTransparencyEntry(gParticleConstantBuffer.ScreenSize, uint2(x, y), texColor, vertexPos.z);
		}
	}

#ifdef _DEBUG
	uint foo;
	AtomicAdd(gParticleStatsBufferRW[0].VisibleParticlesCount, 1, foo);
	AtomicAdd(gParticleStatsBufferRW[0].SoftwareRasterized, 1, foo);
#endif

	RETURN();
}
