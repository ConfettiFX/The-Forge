/*
 * Copyright (c) 2017-2025 The Forge Interactive Inc.
 *
 * This file is part of The-Forge
 * (see https://github.com/ConfettiFX/The-Forge).
 *
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

#include "../../../Graphics/ShaderUtilities.h.fsl"
#include "../../../../../../Common_3/Renderer/ParticleSystem/Shaders/ParticleShared.h.fsl"
#include "../../../../../../Common_3/Renderer/ParticleSystem/Shaders/ParticleSets.h.fsl"
#include "../../../../../../Common_3/Renderer/ParticleSystem/Shaders/ParticlePacking.h.fsl"
#include "ShaderDefs.h.fsl"
#include "VisibilityBufferResources.h.fsl"
#include "Particle.srt.h"

STRUCT(VSOutput)
{
	DATA(float4, Position, SV_Position);
	DATA(float2, TexCoord, TEXCOORD0);
	DATA(FLAT(uint), Color, TEXCOORD1);
	DATA(FLAT(uint), TexIndex, TEXCOORD2);
};

ROOT_SIGNATURE(DefaultRootSignature)
VSOutput VS_MAIN( SV_VertexID(uint) vertexId, SV_InstanceID(uint) instanceId )
{
	INIT_MAIN;

	VSOutput result;
	result.Position = float4(0,0,0,0);
	result.TexCoord = float2(0,0);
	result.Color = 0;
	result.TexIndex = 0;

	uint particleIdx = gParticlesToRasterizeRW[instanceId];

	uint Bitfield = gBitfieldBufferRW[particleIdx];
	ParticleData particleData;
	GetParticleData(particleData, gParticlesDataBufferRW, particleIdx);

	float4 VelocityAge;
	float3 position;

	uint particleSetIdx = Bitfield & PARTICLE_BITFIELD_SET_INDEX_MASK;
	UnpackParticle(gParticleSetsBuffer[particleSetIdx], particleData, position, VelocityAge);

	float scale = UnpackParticleSize(gParticleSetsBuffer[Bitfield & PARTICLE_BITFIELD_SET_INDEX_MASK], particleIdx, UnpackParticleAgeOverMaxAge(particleData));
	if ((Bitfield & PARTICLE_BITFIELD_HAS_COLLIDED) > 0)
		scale *= 20.0f;

	const float3 vertexPosCache_indexed[4] =
	{
		float3(-scale, -scale, 0.0f),
		float3(-scale,  scale, 0.0f),
		float3( scale,  scale, 0.0f),
		float3( scale, -scale, 0.0f),
	};

	const float2 vertexTexCache_indexed[4] =
	{
		float2(0.0f, 1.0f),
		float2(0.0f, 0.0f),
		float2(1.0f, 0.0f),
		float2(1.0f, 1.0f),
	};

	uint vertIndex;
	switch (vertexId)
	{
		case 0: 
		case 1: 
		case 2: vertIndex = vertexId; break;
		case 3: vertIndex = 2; break;
		case 4: vertIndex = 3; break;
		case 5: vertIndex = 0; break;
	}

	float3 right = getCol(gParticleConstantBuffer.ViewTransform, 0).xyz;
	float3 up = getCol(gParticleConstantBuffer.ViewTransform, 1).xyz;
	float3 forward = getCol(gParticleConstantBuffer.ViewTransform, 2).xyz;

	switch (Bitfield & PARTICLE_BITFIELD_BILLBOARD_MODE_BITS_MASK)
	{
		case PARTICLE_BITFIELD_BILLBOARD_MODE_SCREEN_ALIGNED:
		{
			float3 velocityViewSpace = mul(gParticleConstantBuffer.ViewTransform, float4(VelocityAge.xyz, 0.0)).xyz;

			result.Position.xyz = vertexPosCache_indexed[vertIndex].xyz;
			result.Position.xyz += dot(result.Position.xyz, velocityViewSpace) * velocityViewSpace * gParticleSetsBuffer[particleSetIdx].VelocityStretch;
			result.Position += 	mul(gParticleConstantBuffer.ViewTransform, float4(position, 1.0f));

		} break;
		case PARTICLE_BITFIELD_BILLBOARD_MODE_VELOCITY_ORIENTED:
		{
			up = normalize(VelocityAge.xyz);
			forward = float3(0.0f, 0.0f, 1.0f);
			right = cross(up, forward);
			forward = cross(up, right);
			float4x4 billboard = make_f4x4_cols(float4(right, 0), float4(up, 0), float4(forward, 0), float4(position, 1.0f));
			result.Position = mul(billboard, float4(vertexPosCache_indexed[vertIndex], 1.0));
			result.Position = mul(gParticleConstantBuffer.ViewTransform, result.Position);
		} break;
		case PARTICLE_BITFIELD_BILLBOARD_MODE_HORIZONTAL:
		{
			forward = float3(0.0f, 1.0f, 0.0f);
			up = float3(0.0f, 0.0f, 1.0f);
			right = float3(1.0f, 0.0f, 0.0f);
			float4x4 billboard = make_f4x4_cols(float4(right, 0), float4(up, 0), float4(forward, 0), float4(position, 1.0f));
			result.Position = mul(billboard, float4(vertexPosCache_indexed[vertIndex], 1.0));
			result.Position = mul(gParticleConstantBuffer.ViewTransform, result.Position);
		} break;
		default:
			break;
	}

	uint texIndex = (Bitfield & PARTICLE_BITFIELD_HAS_COLLIDED) > 0 ? UnpackSecondaryTextureIndex(gParticleSetsBuffer[particleSetIdx]) : 
		UnpackPrimaryTextureIndex(gParticleSetsBuffer[particleSetIdx]);
	float2 tileOffset = float2(0,0);
	uint2 tileAmount = uint2(1,1);
	
	if ((Bitfield & PARTICLE_BITFIELD_HAS_COLLIDED) > 0)
	{
		uint frameIndex = uint(particleData.AnimationTime * UnpackAnimationFrameCount(gParticleSetsBuffer[particleSetIdx]));
		tileAmount = uint2(UnpackAnimationTilesX(gParticleSetsBuffer[particleSetIdx]), UnpackAnimationTilesY(gParticleSetsBuffer[particleSetIdx]));
		frameIndex = (Bitfield & PARTICLE_BITFIELD_LOOP_ANIMATION) > 0 ? (frameIndex % (tileAmount.x * tileAmount.y)) : clamp(frameIndex, uint(0), tileAmount.x * tileAmount.y);

		tileOffset = float2(frameIndex % tileAmount.x, frameIndex / tileAmount.x);
		tileOffset /= float2(tileAmount);
	}

	float ageOverMaxAge = UnpackParticleAgeOverMaxAge(particleData);
	float2 minMaxSpeed = float2(unpack2Floats(gParticleSetsBuffer[particleSetIdx].SteeringStrengthMinSpeed).y, unpack2Floats(gParticleSetsBuffer[particleSetIdx].MaxSizeAndSpeed).y);
	float speedOverMaxSpeed = UnpackParticleSpeedOverMaxSpeed(particleData, gParticleSetsBuffer[particleSetIdx], minMaxSpeed.x, minMaxSpeed.y);
	float4 col = (gParticleSetsBuffer[particleSetIdx].ParticleSetBitfield & PARTICLE_BITFIELD_MODULATION_TYPE_LIFETIME) > 0 ?
		UnpackParticleColorLifetime(gParticleSetsBuffer[particleSetIdx].StartColor, gParticleSetsBuffer[particleSetIdx].EndColor, ageOverMaxAge) : 
		UnpackParticleColorSpeed(gParticleSetsBuffer[particleSetIdx].StartColor, gParticleSetsBuffer[particleSetIdx].EndColor, 
			ageOverMaxAge, pow(speedOverMaxSpeed, PARTICLE_COLOR_OVER_SPEED_STRENGTH));
	col *= float4(1,1,1,UnpackAlpha(gParticleSetsBuffer[particleSetIdx], particleIdx));

	result.Position = mul(gParticleConstantBuffer.ProjTransform, result.Position);
	result.TexCoord = vertexTexCache_indexed[vertIndex] / float2(tileAmount) + tileOffset;
	result.Color = packUnorm4x8(col);
	result.TexIndex = texIndex;

	RETURN(result);
}
