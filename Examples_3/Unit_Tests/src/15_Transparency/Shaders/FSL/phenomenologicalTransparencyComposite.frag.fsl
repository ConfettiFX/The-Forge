/*
* Copyright (c) 2017-2022 The Forge Interactive Inc.
*
* This file is part of The-Forge
* (see https://github.com/ConfettiFX/The-Forge).
*
* Licensed to the Apache Software Foundation (ASF) under one
* or more contributor license agreements.  See the NOTICE file
* distributed with this work for additional information
* regarding copyright ownership.  The ASF licenses this file
* to you under the Apache License, Version 2.0 (the
* "License"); you may not use this file except in compliance
* with the License.  You may obtain a copy of the License at
*
*   http://www.apache.org/licenses/LICENSE-2.0
*
* Unless required by applicable law or agreed to in writing,
* software distributed under the License is distributed on an
* "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
* KIND, either express or implied.  See the License for the
* specific language governing permissions and limitations
* under the License.
*/

STRUCT(VSOutput)
{
	DATA(float4, Position, SV_Position);
	DATA(float4, UV, TEXCOORD0);
#if VR_MULTIVIEW_ENABLED
	DATA(FLAT(uint), ViewID, TEXCOORD1);	
#endif
};

RES(SamplerState,  PointSampler,        UPDATE_FREQ_NONE, s0, binding = 0);
RES(SamplerState,  LinearSampler,       UPDATE_FREQ_NONE, s1, binding = 1);

float MaxComponent(float3 v)
{
	return max(max(v.x, v.y), v.z);
}

float MinComponent(float3 v)
{
	return min(min(v.x, v.y), v.z);
}

#if VR_MULTIVIEW_ENABLED

RES(Tex2DArray(float4), AccumulationTexture, UPDATE_FREQ_NONE, t0, binding = 2);
RES(Tex2DArray(float4), ModulationTexture,   UPDATE_FREQ_NONE, t1, binding = 3);
RES(Tex2DArray(float4), BackgroundTexture,   UPDATE_FREQ_NONE, t2, binding = 4);
#if PT_USE_REFRACTION != 0
RES(Tex2DArray(float4), RefractionTexture,   UPDATE_FREQ_NONE, t3, binding = 5);
#endif

float4 PS_MAIN( VSOutput In )
{
	INIT_MAIN;
	float4 Out;
	float4 modulationAndDiffusion = SampleTex2DArray(Get(ModulationTexture), Get(PointSampler), float3(In.UV.xy, VR_VIEW_ID(In.ViewID)));
	float3 modulation = modulationAndDiffusion.rgb;

	if (MinComponent(modulation) == 1.0f)
	{
		Out = SampleTex2DArray(Get(BackgroundTexture), Get(PointSampler), float3(In.UV.xy, VR_VIEW_ID(In.ViewID)));
		RETURN(Out);
	}

	float4 accumulation = SampleTex2DArray(Get(AccumulationTexture), Get(PointSampler), float3(In.UV.xy, VR_VIEW_ID(In.ViewID)));

	// Handle overflow
	if (isinf(accumulation.a))
		accumulation.a = MaxComponent(accumulation.xyz);
	if (isinf(MaxComponent(accumulation.xyz)))
		accumulation = f4(1.0f);

	// Fake transmission by blending in the background color
	const float epsilon = 0.001f;
	accumulation.rgb *= 0.5f + max(modulation, epsilon) / (2.0f * max(epsilon, MaxComponent(modulation)));

#if PT_USE_REFRACTION != 0
	float2 delta = 3.0f * SampleTex2DArray(Get(RefractionTexture), Get(PointSampler), float3(In.UV.xy, VR_VIEW_ID(In.ViewID))).xy * (1.0f / 8.0f);
#else
	float2 delta = f2(0.0f);
#endif

	float3 background = f3(0.0f);

#if PT_USE_DIFFUSION != 0
	const float pixelDiffusion2 = 256.0f;
	float diffusion2 = modulationAndDiffusion.a * pixelDiffusion2;
	if (diffusion2 > 0)
	{
		int2 backgroundSize = GetDimensions(Get(BackgroundTexture), NO_SAMPLER).xy;

		float kernelRadius = min(sqrt(diffusion2), 32.0f) * 2.0f;
		float mipLevel = max(log(kernelRadius) / log(2.0f), 0.0f);
		if (kernelRadius <= 1)
			mipLevel = 0.0f;

		float2 offset = pow(2.0f, mipLevel) / float2(backgroundSize);
		background += SampleLvlTex3D(Get(BackgroundTexture), Get(LinearSampler), float3(In.UV.xy + delta, VR_VIEW_ID(In.ViewID)), mipLevel).rgb * 0.5f;
		background += SampleLvlTex3D(Get(BackgroundTexture), Get(LinearSampler), float3(In.UV.xy + delta + offset, VR_VIEW_ID(In.ViewID)), mipLevel).rgb * 0.125f;
		background += SampleLvlTex3D(Get(BackgroundTexture), Get(LinearSampler), float3(In.UV.xy + delta - offset, VR_VIEW_ID(In.ViewID)), mipLevel).rgb * 0.125f;
		background += SampleLvlTex3D(Get(BackgroundTexture), Get(LinearSampler), float3(In.UV.xy + delta + float2(offset.x, -offset.y), VR_VIEW_ID(In.ViewID)), mipLevel).rgb * 0.125f;
		background += SampleLvlTex3D(Get(BackgroundTexture), Get(LinearSampler), float3(In.UV.xy + delta + float2(-offset.x, offset.y), VR_VIEW_ID(In.ViewID)), mipLevel).rgb * 0.125f;
	}
	else
	{
#endif
#if PT_USE_REFRACTION != 0
	background = SampleLvlTex3D(Get(BackgroundTexture), Get(LinearSampler), float3(clamp(delta + In.UV.xy, 0.001f, 0.999f), VR_VIEW_ID(In.ViewID)), 0.0f).rgb;
#else
	background = SampleLvlTex3D(Get(BackgroundTexture), Get(PointSampler), float3(In.UV.xy, VR_VIEW_ID(In.ViewID)), 0.0f).rgb;
#endif
#if PT_USE_DIFFUSION != 0
	}
#endif

	Out = float4(background * modulation + (1.0f - modulation) * accumulation.rgb / max(accumulation.a, 0.00001f), 1.0f);
	RETURN(Out);
}

#else

RES(Tex2D(float4), AccumulationTexture, UPDATE_FREQ_NONE, t0, binding = 2);
RES(Tex2D(float4), ModulationTexture,   UPDATE_FREQ_NONE, t1, binding = 3);
RES(Tex2D(float4), BackgroundTexture,   UPDATE_FREQ_NONE, t2, binding = 4);
#if PT_USE_REFRACTION != 0
RES(Tex2D(float4), RefractionTexture,   UPDATE_FREQ_NONE, t3, binding = 5);
#endif

float4 PS_MAIN( VSOutput In )
{
	INIT_MAIN;
	float4 Out;
	float4 modulationAndDiffusion = SampleTex2D(Get(ModulationTexture), Get(PointSampler), In.UV.xy);
	float3 modulation = modulationAndDiffusion.rgb;

	if (MinComponent(modulation) == 1.0f)
	{
		Out = SampleTex2D(Get(BackgroundTexture), Get(PointSampler), In.UV.xy);
		RETURN(Out);
	}

	float4 accumulation = SampleTex2D(Get(AccumulationTexture), Get(PointSampler), In.UV.xy);

	// Handle overflow
	if (isinf(accumulation.a))
		accumulation.a = MaxComponent(accumulation.xyz);
	if (isinf(MaxComponent(accumulation.xyz)))
		accumulation = f4(1.0f);

	// Fake transmission by blending in the background color
	const float epsilon = 0.001f;
	accumulation.rgb *= 0.5f + max(modulation, epsilon) / (2.0f * max(epsilon, MaxComponent(modulation)));

#if PT_USE_REFRACTION != 0
	float2 delta = 3.0f * SampleTex2D(Get(RefractionTexture), Get(PointSampler), In.UV.xy).xy * (1.0f / 8.0f);
#else
	float2 delta = f2(0.0f);
#endif

	float3 background = f3(0.0f);

#if PT_USE_DIFFUSION != 0
	const float pixelDiffusion2 = 256.0f;
	float diffusion2 = modulationAndDiffusion.a * pixelDiffusion2;
	if (diffusion2 > 0)
	{
		int2 backgroundSize = GetDimensions(Get(BackgroundTexture), NO_SAMPLER);

		float kernelRadius = min(sqrt(diffusion2), 32.0f) * 2.0f;
		float mipLevel = max(log(kernelRadius) / log(2.0f), 0.0f);
		if (kernelRadius <= 1)
			mipLevel = 0.0f;

		float2 offset = pow(2.0f, mipLevel) / float2(backgroundSize);
		background += SampleLvlTex2D(Get(BackgroundTexture), Get(LinearSampler), In.UV.xy + delta, mipLevel).rgb * 0.5f;
		background += SampleLvlTex2D(Get(BackgroundTexture), Get(LinearSampler), In.UV.xy + delta + offset, mipLevel).rgb * 0.125f;
		background += SampleLvlTex2D(Get(BackgroundTexture), Get(LinearSampler), In.UV.xy + delta - offset, mipLevel).rgb * 0.125f;
		background += SampleLvlTex2D(Get(BackgroundTexture), Get(LinearSampler), In.UV.xy + delta + float2(offset.x, -offset.y), mipLevel).rgb * 0.125f;
		background += SampleLvlTex2D(Get(BackgroundTexture), Get(LinearSampler), In.UV.xy + delta + float2(-offset.x, offset.y), mipLevel).rgb * 0.125f;
	}
	else
	{
#endif
#if PT_USE_REFRACTION != 0
	background = SampleLvlTex2D(Get(BackgroundTexture), Get(LinearSampler), clamp(delta + In.UV.xy, 0.001f, 0.999f), 0.0f).rgb;
#else
	background = SampleLvlTex2D(Get(BackgroundTexture), Get(PointSampler), In.UV.xy, 0.0f).rgb;
#endif
#if PT_USE_DIFFUSION != 0
	}
#endif

	Out = float4(background * modulation + (1.0f - modulation) * accumulation.rgb / max(accumulation.a, 0.00001f), 1.0f);
	RETURN(Out);
}
#endif