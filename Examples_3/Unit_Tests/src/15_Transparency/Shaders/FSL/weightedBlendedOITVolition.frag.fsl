/*
* Copyright (c) 2017-2022 The Forge Interactive Inc.
*
* This file is part of The-Forge
* (see https://github.com/ConfettiFX/The-Forge).
*
* Licensed to the Apache Software Foundation (ASF) under one
* or more contributor license agreements.  See the NOTICE file
* distributed with this work for additional information
* regarding copyright ownership.  The ASF licenses this file
* to you under the Apache License, Version 2.0 (the
* "License"); you may not use this file except in compliance
* with the License.  You may obtain a copy of the License at
*
*   http://www.apache.org/licenses/LICENSE-2.0
*
* Unless required by applicable law or agreed to in writing,
* software distributed under the License is distributed on an
* "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
* KIND, either express or implied.  See the License for the
* specific language governing permissions and limitations
* under the License.
*/

#define VOLITION
#include "shaderDefs.h.fsl"
#include "shading.h.fsl"

STRUCT(VSOutput)
{
	DATA(float4, Position, SV_Position);
	DATA(float4, WorldPosition, POSITION);
	DATA(float4, Normal, NORMAL);
	DATA(float4, UV, TEXCOORD0);
	DATA(FLAT(uint), MatID, MAT_ID);
};

STRUCT(PSOutput)
{
	DATA(float4, Accumulation, SV_Target0);
	DATA(float4, Revealage,    SV_Target1);
};

void weighted_oit_process(out(float4) accum, out(float) revealage, out(float) emissive_weight, float4 premultiplied_alpha_color, float raw_emissive_luminance, float view_depth, float current_camera_exposure)
{
	// Exposure changes relative importance of emissive luminance (whereas it does not for opacity)
	float relative_emissive_luminance = raw_emissive_luminance * current_camera_exposure;

	//Emissive sensitivity is hard to pin down
	//On the one hand, we want a low sensitivity so we don't get dark halos around "feathered" emissive alpha that overlap with eachother
	//On the other hand, we want a high sensitivity so that dim emissive holograms don't get overly downweighted.
	//We expose this to the artist to let them choose what is more important.
	const float emissive_sensitivity = 1.0/Get(emissiveSensitivityValue);

	float clamped_emissive = saturate(relative_emissive_luminance);
	float clamped_alpha = saturate(premultiplied_alpha_color.a);

	// Intermediate terms to be cubed
	// NOTE: This part differs from McGuire's sample code:
	// since we're using premultiplied alpha in the composite, we want to
	// keep emissive values that have low coverage weighted appropriately
	// so, we'll add the emissive luminance to the alpha when computing the alpha portion of the weight
	// NOTE: We also don't add a small value to a, we allow it to go all the way to zero, so that completely invisible portions do not influence the result
	float a = saturate((clamped_alpha*Get(opacitySensitivity)) + (clamped_emissive*emissive_sensitivity));

	// NOTE: This differs from McGuire's sample code. In order to avoid having to tune the algorithm separately for different
	// near/far plane values, we produce a "canonical" depth value from the view-depth, using an fixed near plane and a tunable far plane
	const float canonical_near_z = 0.5;
	const float canonical_far_z = 300.0;
	float range = canonical_far_z-canonical_near_z;
	float canonical_depth = saturate(canonical_far_z/range - (canonical_far_z*canonical_near_z)/(view_depth*range));
	float b = 1.0 - canonical_depth;

	// clamp color to combat overflow (weight will be clamped too)
	float3 clamped_color = min(premultiplied_alpha_color.rgb, Get(maximumColorValue));

	float w = Get(precisionScalar) * b * b * b; //basic depth based weight
	w += Get(weightBias); //NOTE: This differs from McGuire's code. It is an alternate way to prevent underflow and limits near/far weight ratio
	w = min(w, Get(maximumWeight)); //clamp by maximum weight BEFORE multiplying by opacity weight (so that we'll properly reduce near faint stuff in weight)
	w *= a * a * a; //incorporate opacity weight as the last step

	accum = float4(clamped_color*w, w); //NOTE: This differs from McGuire's sample code because we want to be able to handle fully additive alpha (e.g. emissive), which has a coverage of 0 (revealage of 1.0)
	revealage = clamped_alpha; //blend state will invert this to produce actual revealage
	emissive_weight = saturate(relative_emissive_luminance*Get(additiveSensitivity))/8.0f; //we're going to store this into an 8-bit channel, so we divide by the maximum number of additive layers we can support
}

PSOutput PS_MAIN( VSOutput In )
{    
	INIT_MAIN;
	PSOutput Out;

	float4 finalColor = Shade(In.MatID, In.UV.xy, In.WorldPosition.xyz, normalize(In.Normal.xyz));

	float d = In.Position.z / In.Position.w;
	float4 premultipliedColor = float4(finalColor.rgb * finalColor.a, finalColor.a);
	float emissiveLuminance = dot(finalColor.rgb, float3(0.2126f, 0.7152f, 0.0722f));

	Out.Revealage = f4(0.0f);
	float revealage = 0, emissiveWeight = 0;
	weighted_oit_process(Out.Accumulation, revealage, emissiveWeight, premultipliedColor, emissiveLuminance, d, 1.0f);
	Out.Revealage.x = revealage;
	Out.Revealage.w = emissiveWeight;

	RETURN(Out);
}
