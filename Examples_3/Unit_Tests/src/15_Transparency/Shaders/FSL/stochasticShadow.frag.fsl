/*
* Copyright (c) 2017-2022 The Forge Interactive Inc.
*
* This file is part of The-Forge
* (see https://github.com/ConfettiFX/The-Forge).
*
* Licensed to the Apache Software Foundation (ASF) under one
* or more contributor license agreements.  See the NOTICE file
* distributed with this work for additional information
* regarding copyright ownership.  The ASF licenses this file
* to you under the Apache License, Version 2.0 (the
* "License"); you may not use this file except in compliance
* with the License.  You may obtain a copy of the License at
*
*   http://www.apache.org/licenses/LICENSE-2.0
*
* Unless required by applicable law or agreed to in writing,
* software distributed under the License is distributed on an
* "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
* KIND, either express or implied.  See the License for the
* specific language governing permissions and limitations
* under the License.
*/

#include "shaderDefs.h.fsl"
#include "shading.h.fsl"

// source: https://www.shadertoy.com/view/4sfGzS
float hash(float3 p)
{
	p = frac(p*0.3183099 + .1);
	p *= 17.0;
	return frac(p.x*p.y*p.z*(p.x + p.y + p.z));
}

float noise(in(float3) x)
{
	float3 p = floor(x);
	float3 f = frac(x);
	f = f * f*(3.0 - 2.0*f);

	return lerp(lerp(lerp(hash(p + float3(0, 0, 0)),
		hash(p + float3(1, 0, 0)), f.x),
		lerp(hash(p + float3(0, 1, 0)),
		hash(p + float3(1, 1, 0)), f.x), f.y),
		lerp(lerp(hash(p + float3(0, 0, 1)),
		hash(p + float3(1, 0, 1)), f.x),
		lerp(hash(p + float3(0, 1, 1)),
		hash(p + float3(1, 1, 1)), f.x), f.y), f.z);
}

STRUCT(VSOutput)
{
	DATA(float4, Position, SV_Position);
	DATA(float4, WorldPosition, POSITION);
	DATA(float4, Normal, NORMAL);
	DATA(float4, UV, TEXCOORD0);
	DATA(FLAT(uint), MatID, MAT_ID);
};

STRUCT(PSOutput)
{
	DATA(float2, RedVarianceShadowMap,   SV_Target0);
	DATA(float2, GreenVarianceShadowMap, SV_Target1);
	DATA(float2, BlueVarianceShadowMap,  SV_Target2);
};

PSOutput PS_MAIN( VSOutput In )
{ 
	INIT_MAIN;
	PSOutput Out;
	Material mat = Get(Materials)[In.MatID];
	float4 matColor = f4(0);
	uint index = mat.AlbedoTexID;
	BeginNonUniformResourceIndex(index, MAX_NUM_TEXTURES);
		matColor = (mat.TextureFlags & 1) != 0 ? SampleTex2D(Get(MaterialTextures)[index], Get(LinearSampler), In.UV.xy) : mat.Color;
	EndNonUniformResourceIndex();
	float3 p = float3(1.0f - mat.Transmission.xyz) * matColor.a;
	float e = noise(In.WorldPosition.xyz * 10000.0f);

	float3 normal = normalize(In.Normal.xyz);

	float3 ld = float3(Get(camViewMat)[2][0], Get(camViewMat)[2][1], Get(camViewMat)[2][2]);
	float s = saturate((mat.RefractionRatio - 1.0f) * 0.5f);
	float g = 2.0f * saturate(1.0f - pow(dot(normalize(normal), -ld.xyz), 128.0f * s*s)) - 1.0f;
	p = min(f3(1.0f), (1.0f + g * pow(s, 0.2f)) * p);


	float2 moments = ComputeMoments(In.Position.z);
	Out.RedVarianceShadowMap   = max(moments, f2(e > p.r));
	Out.GreenVarianceShadowMap = max(moments, f2(e > p.g));
	Out.BlueVarianceShadowMap  = max(moments, f2(e > p.b));
	RETURN(Out);
}
