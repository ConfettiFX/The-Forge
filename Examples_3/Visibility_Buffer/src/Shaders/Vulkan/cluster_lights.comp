#version 450 core
#if !defined(WINDOWS) && !defined(ANDROID) && !defined(LINUX)
#define WINDOWS 	// Assume windows if no platform define has been added to the shader
#endif


/*
 * Copyright (c) 2018-2019 Confetti Interactive Inc.
 * 
 * This file is part of The-Forge
 * (see https://github.com/ConfettiFX/The-Forge).
 * 
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 * 
 *   http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
*/


#extension GL_GOOGLE_include_directive : enable

#include "shading.h"

// This compute shader determines if a light of index groupId overlaps
// the cluster (thread.x,thread.y). Then the light is added to the cluster.

layout(std430, set = 0, binding = 0) restrict buffer lightClustersCount
{
	uint lightClustersCountBuffer[];
};

layout(std430, set = 0, binding = 1) restrict buffer lightClusters
{
	uint lightClustersBuffer[];
};

layout(set = 0, binding = 2) uniform uniforms
{
	PerFrameConstants uniformsData;
};

layout(std430, set = 0, binding = 3) restrict readonly buffer lights
{
	LightData lightsBuffer[];
};

layout (local_size_x = LIGHT_CLUSTER_WIDTH, local_size_y = LIGHT_CLUSTER_HEIGHT, local_size_z = 1) in;
void main()
{
	uvec3 threadInGroupId  = gl_LocalInvocationID;
	uvec3 groupId = gl_WorkGroupID;
	const float invClusterWidth = 1.0f / float(LIGHT_CLUSTER_WIDTH);
	const float invClusterHeight = 1.0f / float(LIGHT_CLUSTER_HEIGHT);
	const vec2 windowSize = uniformsData.cullingViewports[VIEW_CAMERA].windowSize;
	
	const float aspectRatio = windowSize.x / windowSize.y;
	
	LightData lightData = lightsBuffer[groupId.x];
	
	vec4 lightPosWorldSpace = vec4(lightData.position.x, lightData.position.y, lightData.position.z, 1);
	vec4 lightPosClipSpace = uniformsData.transform[VIEW_CAMERA].mvp * lightPosWorldSpace;
	float invLightPosW = 1.0f / lightPosClipSpace.w;
	vec3 lightPos = lightPosClipSpace.xyz * invLightPosW;
	
	// Early exit light if it's behind the camera
	if (lightPos.z < 0.0f)
		return;
	
	float projRadius = LIGHT_SIZE * invLightPosW / 0.5f;    
	lightPos *= vec3(aspectRatio, 1.0f, 1.0f);
	
	// Cluster coordinates in post perspective clip space
	float clusterLeft = float(threadInGroupId.x) * invClusterWidth;
	float clusterTop =  float(threadInGroupId.y) * invClusterHeight;
	float clusterRight = clusterLeft + invClusterWidth;
	float clusterBottom = clusterTop + invClusterHeight;
	
	// Transform coordinates from range [0..1] to range [-1..1]
	clusterLeft = clusterLeft*2.0f - 1.0f;
	clusterTop = clusterTop*2.0f - 1.0f;
	clusterRight = clusterRight*2.0f - 1.0f;
	clusterBottom = clusterBottom*2.0f - 1.0f;
	
	clusterLeft *= aspectRatio;
	clusterRight *= aspectRatio;
	
	float clusterCenterX = (clusterLeft + clusterRight) * 0.5f;
	float clusterCenterY = (clusterTop + clusterBottom) * 0.5f;
	float clusterRadius = distance(vec2(clusterLeft, clusterTop), vec2(clusterRight, clusterBottom)) * 0.5f;
	
	// Check if the light projection overlaps the cluster: add the light bit to this cluster coords
	float distanceToCenter = distance(vec2(clusterCenterX, clusterCenterY), lightPos.xy);
	if (distanceToCenter  < projRadius + clusterRadius)
	{
		// Increase light count on this cluster
		uint lightArrayPos = atomicAdd(lightClustersCountBuffer[LIGHT_CLUSTER_COUNT_POS(threadInGroupId.x, threadInGroupId.y)], 1);

		// Add light id to cluster
		atomicExchange(lightClustersBuffer[LIGHT_CLUSTER_DATA_POS(lightArrayPos, threadInGroupId.x, threadInGroupId.y)], groupId.x);
	}
}
